
&ИзменениеИКонтроль("ЗаполнитьРеквизитыШапкиУПД")
Процедура ЕТС_ЗаполнитьРеквизитыШапкиУПД(ДанныеПечати, СведенияОПоставщике, ДанныеКонтрагентов, ОбластьМакета, ДанныеПоставщиков, ОпцииПечати)

	Если ДанныеПечати.Исправление Тогда
		ДатаСведений = ДанныеПечати.ДатаИсправления;
	Иначе
		ДатаСведений = ДанныеПечати.Дата;
	КонецЕсли;

	СведенияОГрузоотправителе = СведенияОГрузоотправителе(ДанныеПечати, ДатаСведений);
	ВыводитьДанныеПоставщика = Не (ДанныеПечати.СтатусУПД = 2 И ДанныеПечати.ЭтоПередачаНаКомиссию);
	ВыводитьДанныеПокупателя = Не (ДанныеПечати.СтатусУПД = 2 И ДанныеПечати.ЭтоПередачаНаКомиссию);

	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);

	ПараметрыШапки = Новый Структура;

	Если ОпцииПечати.НомерСформированВСчетеФактуре Тогда
		ПараметрыШапки.Вставить("Номер", ДанныеПечати.Номер);
	Иначе
		ПараметрыШапки.Вставить("Номер", НомерСчетаФактурыНаПечать(ДанныеПечати.Номер, ДанныеПечати.ИндексПодразделения));
	КонецЕсли;

	ПараметрыШапки.Вставить("Дата", Формат(ДанныеПечати.Дата, "ДЛФ=ДД"));
	ПараметрыШапки.Вставить("НомерИсправления", ?(ДанныеПечати.Исправление, ДанныеПечати.НомерИсправления, "--"));
	ПараметрыШапки.Вставить("ДатаИсправления",
	?(ДанныеПечати.Исправление, Формат(ДанныеПечати.ДатаИсправления, "ДЛФ=ДД"), "--"));

	Если ОпцииПечати.ДействующиеПостановления.Постановление2026 Тогда 
		ПараметрыШапки.Вставить("НомерПостановления", УчетНДСПереопределяемый.НомерПостановления2026());
		ПараметрыШапки.Вставить("ДатаПостановления", УчетНДСПереопределяемый.ДатаПостановления2026());
	КонецЕсли;

	// Выводим данные о поставщике.

	Если ДанныеПоставщиков = Неопределено Тогда

		ПредставлениеПоставщика = "--";
		ПредставлениеАдресПоставщика = "--";
		ПредставлениеИННПоставщика = "--";

		Если ВыводитьДанныеПоставщика Тогда
			ПредставлениеПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1'"),
			СведенияОПоставщике.ПолноеНаименование);

			ПредставлениеАдресПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1'"),
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщике, "ЮридическийАдрес"));

			ПредставлениеИННПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1%2'"),
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщике, "ИНН", Ложь),
			?(Не ПустаяСтрока(ДанныеПечати.КПППоставщика), "/" + ДанныеПечати.КПППоставщика, ""));
		КонецЕсли;

		// Выводим данные грузоотправителя.
		ТекстГрузоотправителя = "";
		Если ДанныеПечати.ТолькоУслуги ИЛИ ДанныеПечати.Грузоотправитель = Неопределено Тогда
			ТекстГрузоотправителя = "--";
		ИначеЕсли ВыводитьДанныеПоставщика
			И (ДанныеПечати.Организация = ДанныеПечати.Грузоотправитель
			ИЛИ ТипЗнч(ДанныеПечати.Грузоотправитель) = Тип("СправочникСсылка.РегистрацииВНалоговомОргане")
			И СведенияОПоставщике.КПП = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеПечати.Грузоотправитель, "КПП")) Тогда
			ТекстГрузоотправителя = НСтр("ru='он же'", Метаданные.Языки.Русский.КодЯзыка);
		Иначе
			ТекстГрузоотправителя = ФормированиеПечатныхФорм.ОписаниеОрганизации(
#Удаление
			СведенияОГрузоотправителе, "ПолноеНаименование,ФактическийАдрес");
#КонецУдаления
#Вставка
			СведенияОГрузоотправителе, "ПолноеНаименование,ИНН,КПП,ФактическийАдрес");
#КонецВставки
		КонецЕсли;

		ПредставлениеГрузоотправителя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		ТекстГрузоотправителя);

	Иначе

		ПредставлениеПоставщика       = "";
		ПредставлениеАдресПоставщика = "";
		ПредставлениеИННПоставщика = "";
		ПредставлениеГрузоотправителя  = "";

		ЕстьГрузоотправитель = НЕ ДанныеПечати.ТолькоУслуги;

		ТаблицаПоставщиков = ТаблицаПоставщиковСчетаФактуры(ДанныеПечати, ДанныеПоставщиков, ДатаСведений);
		Для Каждого СтрокаТаблицы Из ТаблицаПоставщиков Цикл

			Если ВыводитьДанныеПоставщика Тогда
				СведенияОПоставщикеВрем = СтрокаТаблицы.СведенияОПоставщике;

				ПредставлениеПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1; %2'"),
				ПредставлениеПоставщика,
				ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщикеВрем, "ПолноеНаименование", Ложь));

				ПредставлениеАдресПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1; %2'"),
				ПредставлениеАдресПоставщика,
				ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщикеВрем, "ЮридическийАдрес", Ложь));

				ПредставлениеИННПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1; %2%3'"),
				ПредставлениеИННПоставщика,
				СтрокаТаблицы.ИНН,
				?(Не ПустаяСтрока(СтрокаТаблицы.КПП), "/" + СтрокаТаблицы.КПП, ""));
			КонецЕсли;

			Если ЕстьГрузоотправитель Тогда
				СведенияОГрузоотправителе = СтрокаТаблицы.СведенияОГрузоотправителе;
				ПредставлениеГрузоотправителя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1; %2'"),
				ПредставлениеГрузоотправителя,
				ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОГрузоотправителе, "ПолноеНаименование,ФактическийАдрес", Ложь));
			КонецЕсли;

		КонецЦикла;

		ПредставлениеПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		?(ВыводитьДанныеПоставщика, Сред(ПредставлениеПоставщика, 3), "--"));

		ПредставлениеАдресПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		?(ВыводитьДанныеПоставщика, Сред(ПредставлениеАдресПоставщика, 3), "--"));

		ПредставлениеИННПоставщика = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		?(ВыводитьДанныеПоставщика, Сред(ПредставлениеИННПоставщика, 3), "--"));

		ПредставлениеГрузоотправителя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		?(ЕстьГрузоотправитель, Сред(ПредставлениеГрузоотправителя, 3), "--"));

	КонецЕсли;

	ПараметрыШапки.Вставить("ПредставлениеПоставщика", ПредставлениеПоставщика);
	ПараметрыШапки.Вставить("АдресПоставщика", ПредставлениеАдресПоставщика);
	ПараметрыШапки.Вставить("ИННПоставщика", ПредставлениеИННПоставщика);
	ПараметрыШапки.Вставить("ПредставлениеГрузоотправителя", ПредставлениеГрузоотправителя);

	// Выводим данные грузополучателя и покупателя.
	ТекстГрузополучателя = "--";

	ПредставлениеГрузополучателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	ТекстГрузополучателя);
	СтрокаПоДокументу = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	?(ПустаяСтрока(ДанныеПечати.СтрокаПоДокументу),
	НСтр("ru='-- от --'", Метаданные.Языки.Русский.КодЯзыка),
	ДанныеПечати.СтрокаПоДокументу));

	ПараметрыШапки.Вставить("ПоДокументу", СтрокаПоДокументу);

	ЕстьГрузополучатель = Не ДанныеПечати.ТолькоУслуги;

	ТаблицаКонтрагентов = ТаблицаКонтрагентовСчетаФактуры(ДанныеПечати, ДанныеКонтрагентов);

	ПредставлениеПокупателя       = "";
	ПредставлениеАдресаПокупателя = "";
	ПредставлениеИННПокупателя    = "";
	ПредставлениеГрузополучателя  = "";

	Для Каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл

		Если ВыводитьДанныеПокупателя Тогда
			СведенияОПокупателе = СтрокаТаблицы.СведенияОПокупателе;

			ПредставлениеПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1; %2'"),
			ПредставлениеПокупателя,
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПокупателе, "ПолноеНаименование", Ложь));

			ПредставлениеАдресаПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1; %2'"),
			ПредставлениеАдресаПокупателя,
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПокупателе, "ЮридическийАдрес", Ложь));

			ПредставлениеИННПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1; %2%3'"),
			ПредставлениеИННПокупателя,
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПокупателе, "ИНН", Ложь),
			?(Не ПустаяСтрока(СтрокаТаблицы.КПП), "/" + СтрокаТаблицы.КПП, ""));
		КонецЕсли;

		Если ЕстьГрузополучатель Тогда

			СведенияОГрузополучателе = СтрокаТаблицы.СведенияОГрузополучателе;

			Документ = ?(
			ТипЗнч(ДанныеПечати.Ссылка) = Тип("ДокументСсылка.СчетФактураВыданный"),
			ДанныеПечати.ДокументОснование,
			ДанныеПечати.Ссылка);

			Если ПродажиСервер.АдресГрузополучателяБратьИзАдресаДоставки(Документ)
				И ЗначениеЗаполнено(ДанныеПечати.АдресДоставки) Тогда
				СведенияОГрузополучателе.ФактическийАдрес = ДанныеПечати.АдресДоставки;
			КонецЕсли;

			ПредставлениеГрузополучателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='%1; %2'"),
			ПредставлениеГрузополучателя,
			ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОГрузополучателе, "ПолноеНаименование,ФактическийАдрес", Ложь));

		КонецЕсли;

	КонецЦикла;

	ПредставлениеПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	?(ВыводитьДанныеПокупателя, Сред(ПредставлениеПокупателя, 3), "--"));

	ПредставлениеАдресаПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	?(ВыводитьДанныеПокупателя, Сред(ПредставлениеАдресаПокупателя, 3), "--"));

	ПредставлениеИННПокупателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	?(ВыводитьДанныеПокупателя, Сред(ПредставлениеИННПокупателя, 3), "--"));

	ПредставлениеГрузополучателя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"),
	?(ЕстьГрузополучатель, Сред(ПредставлениеГрузополучателя, 3), "--"));

	ПараметрыШапки.Вставить("ПредставлениеПокупателя", ПредставлениеПокупателя);
	ПараметрыШапки.Вставить("АдресПокупателя", ПредставлениеАдресаПокупателя);
	ПараметрыШапки.Вставить("ИННПокупателя", ПредставлениеИННПокупателя);
	ПараметрыШапки.Вставить("ПредставлениеГрузополучателя", ПредставлениеГрузополучателя);
	ПараметрыШапки.Вставить("Валюта", НСтр("ru='Российский рубль, 643'", Метаданные.Языки.Русский.КодЯзыка));

	СтруктураПараметровИдентификаторГосКонтракта = Новый Структура("ИдентификаторГосКонтракта");
	ЗаполнитьЗначенияСвойств(СтруктураПараметровИдентификаторГосКонтракта, ДанныеПечати);
	Если ДействуетПостановление981(ДанныеПечати.Дата,ДанныеПечати.ДатаИсправления) Тогда
		ПредставлениеИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		СокрЛП(СтруктураПараметровИдентификаторГосКонтракта.ИдентификаторГосКонтракта));
	Иначе
		ПредставлениеИдентификаторГосКонтракта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1'"),
		СокрЛП(СтруктураПараметровИдентификаторГосКонтракта.ИдентификаторГосКонтракта));
	КонецЕсли;
	ПараметрыШапки.Вставить("ИдентификаторГосКонтракта", ПредставлениеИдентификаторГосКонтракта);

	ОбластьМакета.Параметры.Заполнить(ПараметрыШапки);

КонецПроцедуры
