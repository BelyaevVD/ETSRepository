
Функция ЦепочкаСчётФактур(Контрагент, ДатаНачала, ДатаОкончания)
	
	ТипНоменклатура = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","Номенклатура");	
	ТипСписокНоменклатуры = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","СписокНоменклатуры");
	ТипСписокУПД = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","СписокУПД");
	ТипУПД = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","УПД");
	ТипКонтактныеЛица = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","КонтактныеЛица");	
	ТипСписокКонтактныхЛиц = ФабрикаXDTO.Тип("http://services.srv.etsgroup.ru","СписокКонтактныхЛиц");
	 	
	СписокУПД = ФабрикаXDTO.Создать(ТипСписокУПД);
	
	КонтрагентСсылка = Справочники.Контрагенты.НайтиПоРеквизиту("НалоговыйНомер",Контрагент);
	Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентСсылка,"Партнер");
	РольДляПечати = ЕТС_ОбщегоНазначения.ПолучитьЗначениеСлужебнойПеременной(ПредопределенноеЗначение("ПланВидовХарактеристик.ЕТС_СлужебныеПеременные.Подписант"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактныеЛицаПартнеровРолиКонтактногоЛица.Ссылка.Наименование КАК ФИО,
		|	КонтактныеЛицаПартнеровРолиКонтактногоЛица.Ссылка.ДолжностьПоВизитке КАК Должность
		|ИЗ
		|	Справочник.КонтактныеЛицаПартнеров.РолиКонтактногоЛица КАК КонтактныеЛицаПартнеровРолиКонтактногоЛица
		|ГДЕ
		|	КонтактныеЛицаПартнеровРолиКонтактногоЛица.РольКонтактногоЛица = &РольКонтактногоЛица
		|	И КонтактныеЛицаПартнеровРолиКонтактногоЛица.Ссылка.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Партнер);
	Запрос.УстановитьПараметр("РольКонтактногоЛица", РольДляПечати);
	
	Рез = Запрос.Выполнить().Выбрать();
	СписокКонтактныхЛиц = ФабрикаXDTO.Создать(ТипСписокКонтактныхЛиц);
	
	Пока Рез.Следующий() Цикл
		Контакт = ФабрикаXDTO.Создать(ТипКонтактныеЛица);
		Контакт.ФИОПолное = Рез.ФИО;
		ДиректорРодительныйПадеж = ПолучитьСклоненияСтроки(Рез.ФИО, , "ПД=Родительный");
		Если ДиректорРодительныйПадеж.Количество() > 0 Тогда
			Контакт.ФИОПолноеРП = ДиректорРодительныйПадеж[0];
		КонецЕсли;
		Контакт.ФИОСокр = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Рез.ФИО);
		Контакт.Должность = Рез.Должность;
		ДолжностьРодительныйПадеж = ПолучитьСклоненияСтроки(Рез.Должность, , "ПД=Родительный");
		Если ДолжностьРодительныйПадеж.Количество() > 0 Тогда
			Контакт.ДолжностьРП = ДолжностьРодительныйПадеж[0];
		КонецЕсли;
		СписокКонтактныхЛиц.Список.Добавить(Контакт);
	КонецЦикла;
	СписокУПД.Контакты = СписокКонтактныхЛиц;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	""Реализация"" КАК ХозОперацияПредставление,
		|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслугТовары.Ссылка) КАК ДокументОснование,
		|	Контрагенты.НалоговыйНомер КАК КонтрагентКод,
		|	Контрагенты.Наименование КАК КонтрагентНаименование,
		|	"""" КАК НомерИсходногоДокумента,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата КАК ДатаИсходногоДокумента,
		|	РеализацияТоваровУслугТовары.Ссылка.Номер КАК НомерУПД,
		|	РеализацияТоваровУслугТовары.Ссылка.Дата КАК ДатаУПД,
		|	Ложь КАК ЕстьОснование,
		|	СпрНоменклатура.Код КАК НоменклатураКод,
		|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.Производитель) КАК НоменклатураПроизводитель,
		|	СпрНоменклатура.Артикул КАК Артикул,
		|	СпрНоменклатура.Наименование КАК НоменклатураНаименование,
		|	РеализацияТоваровУслугТовары.КоличествоУпаковок - ЕСТЬNULL(ВозвратТоваровОтКлиентаТовары.КоличествоУпаковок, 0) + ЕСТЬNULL(КорректировкаРеализацииРасхождения.КоличествоУпаковок, 0) КАК Количество,
		|	РеализацияТоваровУслугТовары.Цена КАК Цена,
		|	РеализацияТоваровУслугТовары.Сумма - ЕСТЬNULL(ВозвратТоваровОтКлиентаТовары.Сумма, 0) + ЕСТЬNULL(КорректировкаРеализацииРасхождения.Сумма, 0) КАК Сумма,
		|	РеализацияТоваровУслугТовары.СуммаНДС - ЕСТЬNULL(ВозвратТоваровОтКлиентаТовары.СуммаНДС, 0) + ЕСТЬNULL(КорректировкаРеализацииРасхождения.СуммаНДС, 0) КАК НДС,
		|	"""" КАК НоменклатураПроизводительКод,
		|	"""" КАК НомерГТД,
		|	ОтветственныеЛицаОрганизаций.Должность КАК Должность,
		|	ОтветственныеЛицаОрганизаций.ФизическоеЛицо.Наименование КАК ФИО,
		|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
		|	РеализацияТоваровУслугТовары.Ссылка.Организация.Наименование КАК Организация
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО РеализацияТоваровУслугТовары.Ссылка.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО РеализацияТоваровУслугТовары.Номенклатура = СпрНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
		|		ПО РеализацияТоваровУслугТовары.Ссылка.Организация = ОтветственныеЛицаОрганизаций.Владелец
		|			И (ОтветственныеЛицаОрганизаций.ЕТС_Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровОтКлиентаТовары
		|		ПО РеализацияТоваровУслугТовары.Ссылка = ВозвратТоваровОтКлиентаТовары.Ссылка.ДокументРеализации
		|			И РеализацияТоваровУслугТовары.Номенклатура = ВозвратТоваровОтКлиентаТовары.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК КорректировкаРеализацииРасхождения
		|		ПО РеализацияТоваровУслугТовары.Ссылка = КорректировкаРеализацииРасхождения.Ссылка.ДокументОснование
		|			И РеализацияТоваровУслугТовары.Номенклатура = КорректировкаРеализацииРасхождения.Номенклатура
		|ГДЕ
		|	РеализацияТоваровУслугТовары.Ссылка.Проведен
		|	И РеализацияТоваровУслугТовары.Ссылка.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И РеализацияТоваровУслугТовары.Ссылка.Контрагент = &Контрагент
		|ИТОГИ
		|	МАКСИМУМ(ХозОперацияПредставление),
		|	МАКСИМУМ(КонтрагентКод),
		|	МАКСИМУМ(КонтрагентНаименование),
		|	МАКСИМУМ(НомерИсходногоДокумента),
		|	МАКСИМУМ(ДатаИсходногоДокумента),
		|	МАКСИМУМ(НомерУПД),
		|	МАКСИМУМ(ДатаУПД),
		|	МАКСИМУМ(ЕстьОснование),
		|	МАКСИМУМ(Должность),
		|	МАКСИМУМ(ФИО),
		|	МАКСИМУМ(Организация)
		|ПО
		|	ДокументОснование";
	
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("Контрагент", КонтрагентСсылка);
	
	РезУПД = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
	
	Пока РезУПД.Следующий() Цикл
		
		УПД = ФабрикаXDTO.Создать(ТипУПД);
		ЗаполнитьЗначенияСвойств(УПД,РезУПД);
		ДиректорРодительныйПадеж = ПолучитьСклоненияСтроки(РезУПД.ФИО, , "ПД=Дательный");
		Если ДиректорРодительныйПадеж.Количество() > 0 Тогда
			УПД.ФИОРуководитель = ДиректорРодительныйПадеж[0];
		КонецЕсли;
		ДолжностьРодительныйПадеж = ПолучитьСклоненияСтроки(РезУПД.Должность, , "ПД=Дательный");
		Если ДолжностьРодительныйПадеж.Количество() > 0 Тогда
			УПД.ДолжностьРуководитель = ДолжностьРодительныйПадеж[0];
		КонецЕсли;
		
		РезНом = РезУПД.Выбрать();
		СписокНоменклатуры = ФабрикаXDTO.Создать(ТипСписокНоменклатуры);
		
		Пока РезНом.Следующий() Цикл
			попытка
				Номенклатура = ФабрикаXDTO.Создать(ТипНоменклатура);
				ЗаполнитьЗначенияСвойств(Номенклатура,РезНом);
				СписокНоменклатуры.Список.Добавить(Номенклатура);
				Исключение
			конецпопытки; 	
		КонецЦикла;
		УПД.Товары = СписокНоменклатуры;
		СписокУПД.УПД.Добавить(УПД);
		
	КонецЦикла;
	   		
	Возврат СписокУПД;

КонецФункции

