
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыЦен.Ссылка КАК ВидЦены,
		|	ВЫБОР
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА Партнеры.Ссылка
		|		ИНАЧЕ СпрСклады.Ссылка
		|	КОНЕЦ КАК ЕТС_Поставщик,
		|	ВЫБОР
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА Партнеры.Наименование
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
		|			ТОГДА СпрСклады.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Поставщик,
		|	ЕСТЬNULL(СпособыОбеспеченияПотребностей.СрокИсполненияЗаказа, 0) КАК ДнейДоставки,
		|	ЕСТЬNULL(ОстатокНаСкладеПереопределяемый.ВНаличииОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьНаСкладеОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьПоМереПоступленияОстаток, 0) КАК Доступно,
		|	ЕСТЬNULL(ОстатокНаСкладеПереопределяемый.ВНаличииОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьНаСкладеОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьПоМереПоступленияОстаток, 0) > 0 КАК ЕстьОстаток,
		|	ВЫБОР
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА 2
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
		|			ТОГДА ВЫБОР
		|					КОГДА ЕСТЬNULL(ОстатокНаСкладеПереопределяемый.ВНаличииОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьНаСкладеОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьПоМереПоступленияОстаток, 0) > 0
		|						ТОГДА 0
		|					ИНАЧЕ 1
		|				КОНЕЦ
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК ПорядокСортировки
		|ИЗ
		|	Справочник.ВидыЦен КАК ВидыЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(
		|					,
		|					Номенклатура = &Номенклатура
		|						И Характеристика = &Характеристика
		|						И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК ОстатокНаСкладеПереопределяемый
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпособыОбеспеченияПотребностей
		|				ПО ОстатокНаСкладеПереопределяемый.Склад = СпособыОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей
		|					И (СпособыОбеспеченияПотребностей.Подразделение = &Подразделение)
		|			ПО (ОстатокНаСкладеПереопределяемый.Склад = СпрСклады.Ссылка)
		|				И (ОстатокНаСкладеПереопределяемый.ВНаличииОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьНаСкладеОстаток - ОстатокНаСкладеПереопределяемый.РезервироватьПоМереПоступленияОстаток > 0)
		|		ПО ВидыЦен.ЕТС_Склад = СпрСклады.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО ВидыЦен.ЕТС_Партнер = Партнеры.Ссылка
		|ГДЕ
		|	НЕ ВидыЦен.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокСортировки,
		|	ДнейДоставки,
		|	ЕстьОстаток УБЫВ";
	
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Параметры.Характеристика);
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	
	СписокВыбора.Загрузить(Запрос.Выполнить().Выгрузить());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	ОбработатьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработатьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор()
	
	ТекДанные = ЭтаФорма.Элементы.СписокВыбора.ТекущиеДанные;
	СтруктураВозврата = Новый Структура("ВидЦены,ЕТС_Поставщик,ДнейДоставки,Поставщик");
	ЗаполнитьЗначенияСвойств(СтруктураВозврата,ТекДанные);
	ЭтаФорма.Закрыть(СтруктураВозврата);

КонецПроцедуры

