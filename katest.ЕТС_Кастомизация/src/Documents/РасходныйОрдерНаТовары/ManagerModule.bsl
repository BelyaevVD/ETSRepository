
&ИзменениеИКонтроль("ЗаполнитьОтгружаемыеТоварыПоТоварамПоРаспоряжениям")
Процедура ЕТС_ЗаполнитьОтгружаемыеТоварыПоТоварамПоРаспоряжениям(Объект)

	Объект.ОтгружаемыеТовары.Очистить();

	Для Каждого СтрТабл Из Объект.ТоварыПоРаспоряжениям Цикл

		НоваяСтрока = Объект.ОтгружаемыеТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТабл);
		НоваяСтрока.КоличествоУпаковок = СтрТабл.Количество;
  #Вставка
		Если Не ЗначениеЗаполнено(НоваяСтрока.Упаковка) 
			  ИЛИ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Упаковка, "Владелец") = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения Тогда
			НоваяСтрока.Упаковка = ЕТС_ОбщегоНазначения.ПолучитьУпаковкуНоменклатуры(НоваяСтрока.НОменклатура);
		КонецЕсли;
  #КонецВставки
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыРасходныхОрдеров", Новый Структура("Склад", Объект.Склад)) Тогда
			НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
		Иначе
			НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить;
		КонецЕсли;

	КонецЦикла;

	КолонкиСвертки = "Номенклатура, Характеристика, Назначение, Серия, СтатусУказанияСерий, ЭтоУпаковочныйЛист,"
	+ "УпаковочныйЛистРодитель, УпаковочныйЛист, Упаковка, Действие";
	КолонкиСуммы   = "Количество, КоличествоУпаковок";

	Если ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ТаблицаДляСвертки = Объект.ОтгружаемыеТовары.Выгрузить(); // ТаблицаЗначений
		ТаблицаДляСвертки.Свернуть(КолонкиСвертки, КолонкиСуммы);
		Объект.ОтгружаемыеТовары.Загрузить(ТаблицаДляСвертки);
	Иначе
		Объект.ОтгружаемыеТовары.Свернуть(КолонкиСвертки, КолонкиСуммы);
	КонецЕсли;

	Объект.ВсегоМест = УпаковочныеЛистыСервер.КоличествоМестВТЧ(Объект.ОтгружаемыеТовары);

КонецПроцедуры
