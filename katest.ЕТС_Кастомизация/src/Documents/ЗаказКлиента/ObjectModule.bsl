
&ИзменениеИКонтроль("ПередЗаписью")
Процедура ЕТС_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);

	Если ЭтоНовый() И Не ЭтоЗаказКакСчет Тогда 
		// если ЭтоЗаказКакСчет = Истина - тогда реквизит был заполнен ранее в форме – изменять не требуется
		ЭтоЗаказКакСчет  = Не ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента");
	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ЭтоЗаказКакСчет И Статус <> Перечисления.СтатусыЗаказовКлиентов.НеСогласован Тогда

		ТекстОшибки = НСтр("ru = 'Заказ как счет должен иметь статус ""На согласовании"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, , , Отказ);

	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиВПродажах")
		И Не СкидкиРассчитаны
		И ПродажиСервер.ОперацииПередачиТоваров().Найти(ХозяйственнаяОперация) = Неопределено Тогда

		Отказ = Истина;

		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'В документе %1 не рассчитаны автоматические скидки (наценки). Проведение невозможно. Для расчёта автоматических скидок (наценок) проведите документ из формы документа.'"),
		Ссылка);

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		Ссылка,
		,
		,
		Отказ);

	КонецЕсли;

	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);

	ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(ЭтотОбъект, "Товары");

	ОперацииПередачи = ПродажиСервер.ОперацииПередачиТоваров();

	РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Соглашение, "КомиссионныеПродажи25, ХозяйственнаяОперация");
	КомиссионныеПродажи25 = ?(ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
	И (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами")
	ИЛИ (ЗначениеЗаполнено(Соглашение)
	И РеквизитыСоглашения.КомиссионныеПродажи25
	И РеквизитыСоглашения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию)), Истина, Ложь);

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала Тогда
		ВернутьМногооборотнуюТару = Ложь;
		ТребуетсяЗалогЗаТару      = Ложь;

	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
		И КомиссионныеПродажи25 Тогда
		ВернутьМногооборотнуюТару      = Ложь;
		СрокВозвратаМногооборотнойТары = 0;
		ТребуетсяЗалогЗаТару           = Ложь;
	КонецЕсли;

	Если (ОперацииПередачи.Найти(ХозяйственнаяОперация) <> Неопределено
		Или Не ВернутьМногооборотнуюТару)
		И ТребуетсяЗалогЗаТару Тогда

		ТребуетсяЗалогЗаТару = Ложь;

	КонецЕсли;

	СуммаДокумента = ПолучитьСуммуЗаказанныхСтрок();
	СуммаВозвратнойТары = ПолучитьСуммуВозвратнойТары();

	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;

	ДокументСогласован = Согласован;

	ОбщегоНазначенияУТ.ИзменитьПризнакСогласованностиДокумента(
	ЭтотОбъект,
	РежимЗаписи,
	Перечисления.СтатусыЗаказовКлиентов.НеСогласован);

	// Установим дату согласования, если документ согласован
#Удаление
	Если Не ДокументСогласован И Согласован Тогда
#КонецУдаления
#Вставка
// Здесь можно описать новое поведение.
	Если Не ДокументСогласован И Согласован И Не ЗначениеЗаполнено(ДатаСогласования) Тогда

#КонецВставки
		ДатаСогласования = ТекущаяДатаСеанса();
		ВзаиморасчетыСервер.ПриИзмененииДатыСогласования(ЭтотОбъект);
	КонецЕсли;

	ИдентификаторПлатежа = ОбщегоНазначенияУТ.ПолучитьУникальныйИдентификаторПлатежа(ЭтотОбъект);

	ВзаиморасчетыСервер.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи);

	ГрафикИсполненияВДоговоре = Ложь;
	Если ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		И ЗначениеЗаполнено(Договор) Тогда
		ГрафикИсполненияВДоговоре = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ЗаданГрафикИсполнения");
	КонецЕсли;

	Если ОперацииПередачи.Найти(ХозяйственнаяОперация) <> Неопределено
		Или ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным
		Или ГрафикИсполненияВДоговоре Тогда

		СуммаАвансаДоОбеспечения = 0;
		СуммаПредоплатыДоОтгрузки = 0;

	Иначе

		ПродажиСервер.ЗаполнитьСуммыАвансаПредоплаты(ЭтотОбъект);

	КонецЕсли;

	Если Не НеОтгружатьЧастями Тогда
		НоваяДатаОтгрузки = Дата(1,1,1);

		Если Товары.Количество() > 0 Тогда

			Если Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению
				ИЛИ Статус = Перечисления.СтатусыЗаказовКлиентов.КОтгрузке
				ИЛИ Статус = Перечисления.СтатусыЗаказовКлиентов.Закрыт Тогда

				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("Отменено", Ложь);
				СтрокиКОбеспечению = Товары.НайтиСтроки(ПараметрыОтбора);

				Если СтрокиКОбеспечению.Количество() > 0 Тогда

					ТаблицаСтрокКОбеспечению = Товары.Выгрузить(СтрокиКОбеспечению, "ДатаОтгрузки");
					ТаблицаСтрокКОбеспечению.Сортировать("ДатаОтгрузки Возр");
					НоваяДатаОтгрузки = ТаблицаСтрокКОбеспечению[0].ДатаОтгрузки;

				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

		ДатаОтгрузки = НоваяДатаОтгрузки;
	Иначе
		ОбеспечениеВДокументахСервер.ЗаполнитьДатыОтгрузкиВТаблице(ДатаОтгрузки, Товары, "ДатаОтгрузки");
	КонецЕсли;

	// Очистим реквизиты документа не используемые для хозяйственной операции.
	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	Документы.ЗаказКлиента.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
	ХозяйственнаяОперация,
	МассивВсехРеквизитов,
	МассивРеквизитовОперации);
	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(
	ЭтотОбъект,
	МассивВсехРеквизитов,
	МассивРеквизитовОперации);

	МассивРеквизитов = Новый Массив;
	Если ЗначениеЗаполнено(ФормаОплаты) И Не (ФормаОплаты = Перечисления.ФормыОплаты.Наличная) Тогда
		МассивРеквизитов.Добавить("Касса");
	КонецЕсли;

	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(ЭтотОбъект, МассивРеквизитов, Новый Массив);

	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ЗаказКлиента));

	ШаблонНазначения = Документы.ЗаказКлиента.ШаблонНазначения(ЭтотОбъект);

	РеквизитыДляПроверкиНазначения = "НаправлениеДеятельности,Договор,Партнер";
	Справочники.Назначения.ПроверитьЗаполнитьПередЗаписью(Назначение, ШаблонНазначения, ЭтотОбъект, РеквизитыДляПроверкиНазначения, Отказ);

	НоменклатураПартнеровСервер.ЗаполнитьНоменклатуруПартнераПоНоменклатуреВТаблице(Товары, Партнер);
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		НоменклатураПартнеровСервер.ЗаполнитьПустоеСопоставлениеВНоменклатуреПартнераПоНоменклатуреИБ(Товары, Отказ);
	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, Метаданные.Документы.ЗаказКлиента.ТабличныеЧасти.Товары.Имя);
	КонецЕсли;

	ЗаказКлиентаЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

#Удаление
	Если ЭтоНовый() И Не ЗначениеЗаполнено(Автор) Тогда
		Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
#КонецУдаления
#Вставка
    Если ДополнительныеСвойства.Свойство("obmen") Тогда
		//ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
		СуммаДокумента = Товары.Итог("Сумма");	
	КонецЕсли;
	Для каждого Стр Из Товары Цикл
		Если Не ЗначениеЗаполнено(Стр.СуммаНДС) Тогда
			Стр.СуммаНДС = Окр(Стр.Сумма - 100 * Стр.Сумма / (100 + Стр.СтавкаНДС.Ставка), 2, РежимОкругления.Окр15как20);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Стр.ЕТС_ИдентификаторСтрокиСайта) Тогда
			Стр.ЕТС_ИдентификаторСтрокиСайта = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;
#КонецВставки
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура ЕТС_ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению И
						Не Константы.ЕТС_РежимЗагрузкиДанных.Получить() Тогда
		ПараметрыОтбора = Новый Структура("Ссылка,Склад,Назначение,Организация",Ссылка,Склад,Назначение,Организация);
		ЕТС_ОбщиеКомандыСервер.СоздатьЗаказыНаПеремещения(ПараметрыОтбора);
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("obmen") И 
						ДополнительныеСвойства.obmen И 
						Не Константы.ЕТС_РежимЗагрузкиДанных.Получить() Тогда
		Проведен = Ложь;
		ДополнительныеСвойства.Удалить("obmen");
		//ДополнительныеСвойства.Вставить("Обработан", Истина);
		Автор = Менеджер;
		Товары.Загрузить(ЕТС_ОбщиеКомандыСервер.ЗаполнитьЗаказКлиента(Ссылка, Истина));
		Попытка
			Записать(РежимЗаписиДокумента.Проведение);
			Проведен = Истина;
		Исключение
		КонецПопытки;
		Если Не Проведен Тогда
			Попытка
				Записать(РежимЗаписиДокумента.Запись);
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура ЕТС_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Статус = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура ЕТС_ОбработкаЗаполнения1(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Автор = Пользователи.АвторизованныйПользователь();
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.Партнеры") Тогда
		ЗаполнитьДокументНаОснованииПартнера(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.СделкиСКлиентами") Тогда
		ЗаполнитьДокументНаОснованииСделкиПоПродаже(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.СоглашенияСКлиентами") Тогда
		ЗаполнитьДокументНаОснованииИндивидуальногоСоглашенияСКлиентом(ДанныеЗаполнения);
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаданиеТорговомуПредставителю") Тогда
		ЗаполнитьДокументНаОснованииЗаданияТорговомуПредставителю(ДанныеЗаполнения);
	Иначе
		Продажи.ПриОбработкеЗаполненияЗаказаКлиента(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
	РеквизитыСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Соглашение, "КомиссионныеПродажи25, ХозяйственнаяОперация");
	КомиссионныеПродажи25 = ?(ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
		И (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами")
			ИЛИ (ЗначениеЗаполнено(Соглашение)
				И РеквизитыСоглашения.КомиссионныеПродажи25
				И РеквизитыСоглашения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию)), Истина, Ложь);
			
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
		 И КомиссионныеПродажи25 Тогда
		ВернутьМногооборотнуюТару      = Ложь;
		СрокВозвратаМногооборотнойТары = 0;
		ТребуетсяЗалогЗаТару           = Ложь;
	КонецЕсли;
	
	ИнициализироватьУсловияПродаж();
	
	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияКассыПриФОИспользоватьНесколькоКассЛожь", Ложь);
	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
	ОтветственныеЛицаСервер.УстановитьМенеджера(ЭтотОбъект, ДокументОснование);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьПодчиненныеРеквизитыОбъекта(ЭтотОбъект, "Менеджер",, Истина);
	
	ЗаказКлиентаЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ВзаиморасчетыСервер.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Склад);
#Вставка
	Если ЗначениеЗаполнено(Склад) И ЗначениеЗаполнено(СкладГруппа) Тогда
		СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, СкладГруппа, Товары, Ложь);
	КонецЕсли;
#КонецВставки
#Удаление
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, СкладГруппа, Товары, Ложь);
#КонецУдаления
	
	Приоритет = ЗначениеНастроекПовтИсп.ПолучитьПриоритетПоУмолчанию(Приоритет);

КонецПроцедуры
