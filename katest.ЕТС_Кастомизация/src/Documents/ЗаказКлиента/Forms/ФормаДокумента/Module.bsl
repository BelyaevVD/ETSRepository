
&НаКлиенте
Процедура ЕТС_ТоварыВидЦеныНачалоВыбораПосле(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекСтрока = ЭтаФорма.Элементы.Товары.ТекущиеДанные;
	ДополнительныеПараметры = Новый Структура("Номенклатура,Характеристика,Склад,Подразделение");
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ТекСтрока);
	ДополнительныеПараметры.Подразделение = ЭтаФорма.Объект.Подразделение;
	ДополнительныеПараметры.Склад = ТекСтрока.ЕТС_Поставщик;
	ЕТС_ОбщиеКомандыКлиент.ЗаказКлиента_ЕТС_ТоварыВидЦены_НачалоВыбора(ЭтаФорма, ДополнительныеПараметры);
КонецПроцедуры

&НаСервере
Процедура ЕТС_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	ЕТС_ОбщегоНазначения.ЗаполнитьПоставщикаВЗаказеКлиента(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЕТС_СтатусОбработкаВыбораПосле(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовКлиентов.КОбеспечению") Тогда
		ПоказатьПредупреждение(,"В статусе к обеспечению сформируются документы заказы поставщику и перемещения!",10,"Внимание!");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЕТС_ТоварыПриАктивизацииСтрокиПосле(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьТЗОстаткиНаФорме(Элемент.ТекущиеДанные.Номенклатура);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЗОстаткиНаФорме(Номенклатура)
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗапасыИПотребностиОстатки.Склад.Наименование КАК Склад,
		|	ЗапасыИПотребностиОстатки.ВНаличииОстаток КАК ВНаличии,
		|	ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток + ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток, 0) КАК ВРезерве
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(, Номенклатура = &Номенклатура) КАК ЗапасыИПотребностиОстатки";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	ЭтаФорма.ТаблицаОстатков.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЕТС_ЗаказНарядПриИзменении()
	Элементы.ЕТС_Автомобиль.Видимость 	= Объект.ЕТС_ЗаказНаряд;
	Элементы.ЕТС_Рекомендации.Видимость = Объект.ЕТС_ЗаказНаряд;
	Элементы.ЕТС_Гарантия.Видимость 	= Объект.ЕТС_ЗаказНаряд;
КонецПроцедуры

&НаСервере
&Перед("СтатусПриИзмененииСервер")
Процедура ЕТС_СтатусПриИзмененииСервер()
	Если Не ЗначениеЗаполнено(Объект.Договор) Тогда
		ЗаполнитьДоговорПоУмолчанию();
	КонецЕсли;
КонецПроцедуры
