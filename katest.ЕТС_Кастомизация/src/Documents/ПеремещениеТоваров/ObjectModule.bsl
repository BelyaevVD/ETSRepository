
&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура ЕТС_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;

	Документы.ПеремещениеТоваров.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
	ХозяйственнаяОперация, 
	МассивВсехРеквизитов, 
	МассивРеквизитовОперации);

	МассивНепроверяемыхРеквизитов = Новый Массив;

	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);

	Если ЗначениеЗаполнено(СкладОтправитель) И СкладОтправитель = СкладПолучатель Тогда
#Вставка
		Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами Тогда
#КонецВставки

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Один склад не может быть как отправителем, так и получателем. Измените один из складов.'"),
		ЭтотОбъект,
		"СкладОтправитель",
		,
		Отказ);

#Вставка
		КонецЕсли;
#КонецВставки
	КонецЕсли;

	// Организация-получатель должна быть взаимосвязана с организацией-отправителем по организационной структуре.
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами
		И ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(ОрганизацияПолучатель)
		И Не Справочники.Организации.ОрганизацииВзаимосвязаныПоОрганизационнойСтруктуре(Организация, ОрганизацияПолучатель) Тогда

		ТекстОшибки = НСтр("ru='Организация-получатель должна быть взаимосвязана с организацией-отправителем по организационной структуре.'");

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстОшибки,
		ЭтотОбъект,
		"ОрганизацияПолучатель",
		,
		Отказ);

	КонецЕсли;

	// Если накладная по заказу - то код строки должен быть заполнен.
	Если Не ЗначениеЗаполнено(ЗаказНаПеремещение) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КодСтроки");
	КонецЕсли;

	// Проверка указания характеристик.
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
	НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПеремещениеТоваров),
	Отказ,
	МассивНепроверяемыхРеквизитов);

	ОбщегоНазначенияУТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
	МассивВсехРеквизитов,
	МассивРеквизитовОперации,
	МассивНепроверяемыхРеквизитов);

	ДоставкаТоваров.ПроверитьЗаполнениеРеквизитовДоставки(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПеремещениеПоЗаказам);

	ПараметрыПроверки = УчетНДСУП.ПараметрыПроверкиЗаполненияДокументаПоВидуДеятельностиНДС();
	ПараметрыПроверки.ИмяТабличнойЧасти = "Товары";
	УчетНДСУП.ПроверитьЗаполнениеДокументаПоВидуДеятельностиНДС(
	ЭтотОбъект, 
	ПеремещениеПодДеятельность, 
	ПараметрыПроверки, 
	Отказ);

	Если Не ПеремещениеПоЗаказам
		Или ЗначениеЗаполнено(ЗаказНаПеремещение) Тогда // заказ на перемещение заполнится перед записью документа

		МассивНепроверяемыхРеквизитов.Добавить("Товары.ЗаказНаПеремещение");
	КонецЕсли;

	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

	ПеремещениеТоваровЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

&Перед("ОбработкаПроведения")
Процедура ЕТС_ОбработкаПроведения(Отказ, РежимПроведения)
	Если ЭтотОбъект.СкладПолучатель.СкладОтветственногоХранения Тогда
		Если ЭтотОбъект.СкладПолучатель.ИсточникИнформацииОЦенахДляПечати = Перечисления.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен И
			ЗначениеЗаполнено(ЭтотОбъект.СкладПолучатель.УчетныйВидЦены) Тогда
			Если Не ЗначениеЗаполнено(ЭтотОбъект.ЕТС_УстановкаЦен) ИЛИ (ЗначениеЗаполнено(ЭтотОбъект.ЕТС_УстановкаЦен) И ЭтотОбъект.ЕТС_УстановкаЦен.ПометкаУдаления) Тогда
				ВидЦены = ЭтотОбъект.СкладПолучатель.УчетныйВидЦены;
				ВалютаЦены = ВидЦены.ВалютаЦены;
				Если ЗначениеЗаполнено(ЭтотОбъект.ЕТС_УстановкаЦен) Тогда
					об = ЭтотОбъект.ЕТС_УстановкаЦен.ПолучитьОбъект();
				Иначе
					об = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
				КонецЕсли;
				об.Дата = Дата-5;
				об.Согласован = Истина;
				об.ПометкаУдаления = Ложь;
				об.Статус = Перечисления.СтатусыУстановокЦенНоменклатуры.Согласован;
				об.ВидыЦен.Очистить();
				НоваяСтрока = об.ВидыЦен.Добавить();
				НоваяСтрока.ВидЦены = ВидЦены;
				ТЧТовары = об.Товары2_5;
				ТЧТовары.Очистить();
				Для Каждого стр Из ЭтотОбъект.Товары Цикл 
					НоваяСтрока = ТЧТовары.Добавить(); 
					ЗаполнитьЗначенияСвойств(НоваяСтрока, стр);
					НоваяСтрока.ЦенаИзмененаВручную = Истина;
					НоваяСтрока.Цена = стр.ЕТС_Цена;
					НоваяСтрока.Валюта = ВалютаЦены;
					НоваяСтрока.ВидЦены = ВидЦены;
				КонецЦикла;
				Попытка
					об.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					об.ОбменДанными.Загрузка = Истина;
					об.Записать(РежимЗаписиДокумента.Запись);
				КонецПопытки;
				ЭтотОбъект.ЕТС_УстановкаЦен = об.Ссылка; 
				ЭтотОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
