
&ИзменениеИКонтроль("УстановитьПрефикс")
Процедура ЕТС_УстановитьПрефикс(Источник, Префикс, УстановитьПрефиксИнформационнойБазы, УстановитьПрефиксОрганизации)

	ПрефиксИнформационнойБазы = "";
	ПрефиксОрганизации        = "";

	Если УстановитьПрефиксИнформационнойБазы Тогда

		ПриОпределенииПрефиксаИнформационнойБазы(ПрефиксИнформационнойБазы);

		ДополнитьСтрокуНулямиСлева(ПрефиксИнформационнойБазы, 2);
	КонецЕсли;

	Если УстановитьПрефиксОрганизации Тогда

		Если РеквизитОрганизацияДоступен(Источник) Тогда

			ПриОпределенииПрефиксаОрганизации(
			Источник[ИмяРеквизитаОрганизация(Источник.Метаданные())], ПрефиксОрганизации);
			// Если задана пустая ссылка на организацию.
			Если ПрефиксОрганизации = Ложь Тогда

				ПрефиксОрганизации = "";

			КонецЕсли;

		КонецЕсли;

#Удаление
		ДополнитьСтрокуНулямиСлева(ПрефиксОрганизации, 2);
#КонецУдаления
#Вставка
		ДополнитьСтрокуНулямиСправа(ПрефиксОрганизации, 3);
#КонецВставки
	КонецЕсли;

#Удаление
	ШаблонПрефикса = "[ОР][ИБ]-[Префикс]";
#КонецУдаления
#Вставка
	ШаблонПрефикса = "[ОР][Префикс]";
#КонецВставки
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[ОР]", ПрефиксОрганизации);
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[ИБ]", ПрефиксИнформационнойБазы);
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[Префикс]", Префикс);

	Префикс = ШаблонПрефикса;

#Вставка
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Номенклатура") 
		Или ТипЗнч(Источник) = Тип("СправочникОбъект.Партнеры") Тогда
		Префикс = "КА";
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.РесурсныеСпецификации") Тогда 
		Префикс = "ЕТ";
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриобретениеУслугПрочихАктивов") Тогда
		Приставка = "у";
		Для Каждого стр Из Источник.Расходы Цикл
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(стр.СтатьяРасходов,"ТипРасходов") = Перечисления.ТипыРасходов.ЗакупкаТоваров Тогда
				Приставка = "д";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Префикс = Приставка + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг") Тогда
		Префикс = "т" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
		Префикс = "п" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПроизводствоБезЗаказа") Тогда
		Префикс = "п" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		Префикс = "к" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.СборкаТоваров") Тогда
		Префикс = "к" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВнутреннееПотребление") Тогда
		Префикс = "к" + Префикс;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		ПрефиксПодразделения = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Подразделение,"ЕТС_Префикс"));
		Если ЗначениеЗаполнено(ПрефиксПодразделения) И ПрефиксПодразделения <> "МСК" Тогда
			Префикс = ПрефиксПодразделения;
		КонецЕсли;
	КонецЕсли;
#КонецВставки

КонецПроцедуры

Процедура ДополнитьСтрокуНулямиСправа(Строка, ДлинаСтроки)
	
	Строка = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Строка, ДлинаСтроки, "0", "Справа");
	
КонецПроцедуры
