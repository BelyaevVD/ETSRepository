
Функция ПрочитатьНаСервереТаможеннуюДекларацию(ПараметрыФормирования) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ПараметрыФормирования.УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ЕТС_ВыполнениеВФоне.СформироватьДанныеТаможеннойДекларации",
		ПараметрыФормирования,
		ПараметрыВыполнения);

КонецФункции

Функция ПослеЧтенияНаСервере(АдресВоВременномХранилище) Экспорт 
	
	Результат = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если Результат.Ошибки.Количество() > 0 Тогда
		Для каждого Ошибка Из Результат.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
	КонецЕсли;
	
	Если Результат.Разделы = Неопределено Или Результат.Товары = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Разделы = ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат.Разделы);
	
	Товары = ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат.Товары);
	
	//Проверим номенклатуру на признак ГТД и если он установлен в ложь изменим его на Истина
	Если Результат.Товары.Колонки.Количество() > 0 Тогда
		МассивНоменклатур = Результат.Товары.ВыгрузитьКолонку("Номенклатура");
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Номенклатура.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Ссылка В (&МассивНоменклатур)
			|	И НЕ Номенклатура.ВестиУчетПоГТД";
		
		Запрос.УстановитьПараметр("МассивНоменклатур", МассивНоменклатур);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоменклатураОбъект = Выборка.Ссылка.ПолучитьОбъект();
			НоменклатураОбъект.ВестиУчетПоГТД = Истина;
			НоменклатураОбъект.ДополнительныеСвойства.Вставить("ВебСервис");
			НоменклатураОбъект.Записать();
		КонецЦикла;

	КонецЕсли;
	
	Возврат Новый Структура("Разделы,Товары", Разделы, Товары);

КонецФункции

Функция ДанныеДляПечатиЭтикеток(Идентификатор, ОбъектыПечати, ДополнительныеПараметры) Экспорт
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ОбъектыПечати[0]);
	
	СоответствиеОбъектов = ОбщегоНазначенияУТ.РазложитьМассивСсылокПоТипам(ОбъектыПечати);
	Если СоответствиеОбъектов.Количество() > 1 Тогда
		ТекстСообщения = НСтр("ru = 'Печать этикеток и ценников для нескольких видов документов не поддерживается'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Возврат МенеджерОбъекта.ДанныеДляПечатиЭтикеток(ОбъектыПечати);
	
КонецФункции

Функция ЗаполнитьЗаказКлиента(Ссылка, ВернутьТЗ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Регистратор,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	СУММА(ЗаказКлиентаТовары.Количество) КАК Количество,
		|	ЗаказКлиентаТовары.ВидЦены КАК ВидЦены,
		|	ВЫБОР
		|		КОГДА НЕ ЗаказКлиентаТовары.Склад = ВидыЦен.ЕТС_Склад
		|				И НЕ ВидыЦен.ЕТС_Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ВидыЦен.ЕТС_Склад
		|		ИНАЧЕ ЗаказКлиентаТовары.Склад
		|	КОНЕЦ КАК Склад,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказКлиентаТовары.Упаковка КАК Упаковка,
		|	СУММА(ЗаказКлиентаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	ЗаказКлиентаТовары.Цена КАК Цена,
		|	ЗаказКлиентаТовары.СтавкаНДС КАК СтавкаНДС,
		|	СУММА(ЗаказКлиентаТовары.Сумма) КАК Сумма,
		|	СУММА(ЗаказКлиентаТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ЗаказКлиентаТовары.СуммаСНДС) КАК СуммаСНДС,
		|	ЗаказКлиентаТовары.Обособленно КАК Обособленно,
		|	ЗаказКлиентаТовары.Склад КАК СкладТЧ,
		|	ЗаказКлиентаТовары.ЕТС_КоммерческоеПредложение КАК ЕТС_КоммерческоеПредложение,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Склад = ВидыЦен.ЕТС_Склад
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоТекущийСклад
		|ПОМЕСТИТЬ ВТ_ДанныеТЧ
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО ЗаказКлиентаТовары.ВидЦены = ВидыЦен.Ссылка
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика,
		|	ВЫБОР
		|		КОГДА НЕ ЗаказКлиентаТовары.Склад = ВидыЦен.ЕТС_Склад
		|				И НЕ ВидыЦен.ЕТС_Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ВидыЦен.ЕТС_Склад
		|		ИНАЧЕ ЗаказКлиентаТовары.Склад
		|	КОНЕЦ,
		|	ЗаказКлиентаТовары.Ссылка,
		|	ЗаказКлиентаТовары.ВидЦены,
		|	ЗаказКлиентаТовары.ДатаОтгрузки,
		|	ЗаказКлиентаТовары.Упаковка,
		|	ЗаказКлиентаТовары.Склад,
		|	ЗаказКлиентаТовары.Цена,
		|	ЗаказКлиентаТовары.СтавкаНДС,
		|	ЗаказКлиентаТовары.Обособленно,
		|	ЗаказКлиентаТовары.ЕТС_КоммерческоеПредложение,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Склад = ВидыЦен.ЕТС_Склад
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеТЧ.Номенклатура КАК Номенклатура,
		|	ВТ_ДанныеТЧ.Характеристика КАК Характеристика,
		|	ВТ_ДанныеТЧ.Склад КАК Склад,
		|	ВТ_ДанныеТЧ.ВидЦены КАК ВидЦены,
		|	ВТ_ДанныеТЧ.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ОстатокНаСкладеОстатки.ВНаличииОстаток, 0) < 0
		|			ТОГДА МАКСИМУМ(ЕСТЬNULL(0 - ОстатокНаСкладеОстатки.РезервироватьНаСкладеОстаток - ОстатокНаСкладеОстатки.РезервироватьПоМереПоступленияОстаток, 0))
		|		ИНАЧЕ МАКСИМУМ(ЕСТЬNULL(ОстатокНаСкладеОстатки.ВНаличииОстаток - ОстатокНаСкладеОстатки.РезервироватьНаСкладеОстаток - ОстатокНаСкладеОстатки.РезервироватьПоМереПоступленияОстаток, 0))
		|	КОНЕЦ КАК Доступно,
		|	СУММА(ЕСТЬNULL(ЗапасыИПотребностиДвижения.ВНаличии + ЗапасыИПотребностиДвижения.РезервироватьНаСкладе + ЗапасыИПотребностиДвижения.РезервироватьПоМереПоступления, 0)) КАК ТекДвижения
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(
		|				,
		|				(Номенклатура, Характеристика, Склад) В
		|						(ВЫБРАТЬ
		|							ВТ_ДанныеТЧ.Номенклатура КАК Номенклатура,
		|							ВТ_ДанныеТЧ.Характеристика КАК Характеристика,
		|							ВТ_ДанныеТЧ.Склад КАК Склад
		|						ИЗ
		|							ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ)
		|					И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК ОстатокНаСкладеОстатки
		|		ПО ВТ_ДанныеТЧ.Номенклатура = ОстатокНаСкладеОстатки.Номенклатура
		|			И ВТ_ДанныеТЧ.Характеристика = ОстатокНаСкладеОстатки.Характеристика
		|			И ВТ_ДанныеТЧ.Склад = ОстатокНаСкладеОстатки.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности КАК ЗапасыИПотребностиДвижения
		|		ПО ВТ_ДанныеТЧ.Регистратор = ЗапасыИПотребностиДвижения.Регистратор
		|			И ВТ_ДанныеТЧ.Номенклатура = ЗапасыИПотребностиДвижения.Номенклатура
		|			И ВТ_ДанныеТЧ.Характеристика = ЗапасыИПотребностиДвижения.Характеристика
		|			И ВТ_ДанныеТЧ.Склад = ЗапасыИПотребностиДвижения.Склад
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДанныеТЧ.Номенклатура,
		|	ВТ_ДанныеТЧ.Характеристика,
		|	ВТ_ДанныеТЧ.Склад,
		|	ВТ_ДанныеТЧ.Количество,
		|	ВТ_ДанныеТЧ.ВидЦены,
		|	ОстатокНаСкладеОстатки.ВНаличииОстаток
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.Характеристика КАК Характеристика,
		|	ВТ_Остатки.ВидЦены КАК ВидЦены,
		|	ВТ_Остатки.Склад КАК Склад,
		|	ВТ_Остатки.Количество КАК Количество,
		|	ВТ_Остатки.Доступно + ВТ_Остатки.ТекДвижения КАК Остаток,
		|	ВТ_Остатки.Доступно КАК Доступно
		|ПОМЕСТИТЬ ВТ_Предварительная
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Предварительная.Номенклатура КАК Номенклатура,
		|	ВТ_Предварительная.Характеристика КАК Характеристика,
		|	ВТ_Предварительная.ВидЦены КАК ВидЦены,
		|	ВТ_Предварительная.Склад КАК Поставщик,
		|	ВТ_Предварительная.Количество КАК Количество,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада) КАК ВариантОбеспечения,
		|	ВТ_Предварительная.Доступно КАК Доступно
		|ПОМЕСТИТЬ ВТ_ДопСтроки
		|ИЗ
		|	ВТ_Предварительная КАК ВТ_Предварительная
		|ГДЕ
		|	ВТ_Предварительная.Количество <= ВТ_Предварительная.Остаток
		|	И НЕ ВТ_Предварительная.Остаток = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Предварительная.Номенклатура,
		|	ВТ_Предварительная.Характеристика,
		|	ВТ_Предварительная.ВидЦены,
		|	ВТ_Предварительная.Склад,
		|	ВТ_Предварительная.Количество - ВТ_Предварительная.Остаток,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления),
		|	ВТ_Предварительная.Доступно
		|ИЗ
		|	ВТ_Предварительная КАК ВТ_Предварительная
		|ГДЕ
		|	ВТ_Предварительная.Количество > ВТ_Предварительная.Остаток
		|	И НЕ ВТ_Предварительная.Остаток = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Предварительная.Номенклатура,
		|	ВТ_Предварительная.Характеристика,
		|	ВТ_Предварительная.ВидЦены,
		|	ВТ_Предварительная.Склад,
		|	ВТ_Предварительная.Остаток,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада),
		|	ВТ_Предварительная.Доступно
		|ИЗ
		|	ВТ_Предварительная КАК ВТ_Предварительная
		|ГДЕ
		|	ВТ_Предварительная.Количество > ВТ_Предварительная.Остаток
		|	И НЕ ВТ_Предварительная.Остаток = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Предварительная.Номенклатура,
		|	ВТ_Предварительная.Характеристика,
		|	ВТ_Предварительная.ВидЦены,
		|	ВТ_Предварительная.Склад,
		|	ВТ_Предварительная.Количество,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления),
		|	ВТ_Предварительная.Доступно
		|ИЗ
		|	ВТ_Предварительная КАК ВТ_Предварительная
		|ГДЕ
		|	ВТ_Предварительная.Остаток = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДопСтроки.Номенклатура КАК Номенклатура,
		|	ВТ_ДопСтроки.Характеристика КАК Характеристика,
		|	ВТ_ДопСтроки.ВидЦены КАК ВидЦены,
		|	ВЫБОР
		|		КОГДА ВТ_ДопСтроки.ВидЦены.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА ВТ_ДопСтроки.ВидЦены.ЕТС_Партнер
		|		ИНАЧЕ ВТ_ДопСтроки.ВидЦены.ЕТС_Склад
		|	КОНЕЦ КАК ЕТС_Поставщик,
		|	ВТ_ДопСтроки.Количество КАК Количество,
		|	ВТ_ДопСтроки.Количество КАК КоличествоУпаковок,
		|	ВЫБОР
		|		КОГДА ВТ_ДанныеТЧ.ЭтоТекущийСклад
		|			ТОГДА ВТ_ДопСтроки.ВариантОбеспечения
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления)
		|	КОНЕЦ КАК ВариантОбеспечения,
		|	ВТ_ДанныеТЧ.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ВТ_ДанныеТЧ.Упаковка КАК Упаковка,
		|	ВТ_ДанныеТЧ.Цена КАК Цена,
		|	ВТ_ДанныеТЧ.СкладТЧ КАК Склад,
		|	ВТ_ДанныеТЧ.ЕТС_КоммерческоеПредложение КАК ЕТС_КоммерческоеПредложение,
		|	ВТ_ДанныеТЧ.СтавкаНДС КАК СтавкаНДС,
		|	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.Сумма / ВТ_ДанныеТЧ.Количество * ВТ_ДопСтроки.Количество КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.СуммаНДС / ВТ_ДанныеТЧ.Количество * ВТ_ДопСтроки.Количество КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ(ВТ_ДанныеТЧ.СуммаСНДС / ВТ_ДанныеТЧ.Количество * ВТ_ДопСтроки.Количество КАК ЧИСЛО(15, 2)) КАК СуммаСНДС,
		|	ВТ_ДопСтроки.Доступно КАК Доступно
		|ПОМЕСТИТЬ ВТ_БезПроблемыКопеек
		|ИЗ
		|	ВТ_ДопСтроки КАК ВТ_ДопСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
		|		ПО ВТ_ДопСтроки.Номенклатура = ВТ_ДанныеТЧ.Номенклатура
		|			И ВТ_ДопСтроки.Характеристика = ВТ_ДанныеТЧ.Характеристика
		|			И ВТ_ДопСтроки.ВидЦены = ВТ_ДанныеТЧ.ВидЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЗ_Исходная.Номенклатура КАК Номенклатура,
		|	ТЗ_Исходная.Характеристика КАК Характеристика,
		|	ТЗ_Исходная.ВидЦены КАК ВидЦены,
		|	ТЗ_Исходная.Сумма - ТЗ_Копейки.Сумма КАК СуммаРазница,
		|	ТЗ_Исходная.СуммаНДС - ТЗ_Копейки.СуммаНДС КАК СуммаНДСРазница,
		|	ТЗ_Исходная.СуммаСНДС - ТЗ_Копейки.СуммаСНДС КАК СуммаСНДСРазница
		|ПОМЕСТИТЬ ВТ_КопейкиРазница
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТ_БезПроблемыКопеек.Номенклатура КАК Номенклатура,
		|		ВТ_БезПроблемыКопеек.Характеристика КАК Характеристика,
		|		ВТ_БезПроблемыКопеек.ВидЦены КАК ВидЦены,
		|		СУММА(ВТ_БезПроблемыКопеек.Сумма) КАК Сумма,
		|		СУММА(ВТ_БезПроблемыКопеек.СуммаНДС) КАК СуммаНДС,
		|		СУММА(ВТ_БезПроблемыКопеек.СуммаСНДС) КАК СуммаСНДС
		|	ИЗ
		|		ВТ_БезПроблемыКопеек КАК ВТ_БезПроблемыКопеек
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_БезПроблемыКопеек.Номенклатура,
		|		ВТ_БезПроблемыКопеек.Характеристика,
		|		ВТ_БезПроблемыКопеек.ВидЦены) КАК ТЗ_Копейки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВТ_ДанныеТЧ.Номенклатура КАК Номенклатура,
		|			ВТ_ДанныеТЧ.Характеристика КАК Характеристика,
		|			ВТ_ДанныеТЧ.ВидЦены КАК ВидЦены,
		|			СУММА(ВТ_ДанныеТЧ.Сумма) КАК Сумма,
		|			СУММА(ВТ_ДанныеТЧ.СуммаНДС) КАК СуммаНДС,
		|			ВТ_ДанныеТЧ.СуммаСНДС КАК СуммаСНДС
		|		ИЗ
		|			ВТ_ДанныеТЧ КАК ВТ_ДанныеТЧ
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ВТ_ДанныеТЧ.Номенклатура,
		|			ВТ_ДанныеТЧ.Характеристика,
		|			ВТ_ДанныеТЧ.ВидЦены,
		|			ВТ_ДанныеТЧ.СуммаСНДС) КАК ТЗ_Исходная
		|		ПО (ТЗ_Исходная.Номенклатура = ТЗ_Копейки.Номенклатура)
		|			И (ТЗ_Исходная.Характеристика = ТЗ_Копейки.Характеристика)
		|			И (ТЗ_Исходная.ВидЦены = ТЗ_Копейки.ВидЦены)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_БезПроблемыКопеек.Номенклатура КАК Номенклатура,
		|	ВТ_БезПроблемыКопеек.Характеристика КАК Характеристика,
		|	ВТ_БезПроблемыКопеек.ВидЦены КАК ВидЦены,
		|	ВТ_БезПроблемыКопеек.ЕТС_Поставщик КАК ЕТС_Поставщик,
		|	ВТ_БезПроблемыКопеек.Количество КАК Количество,
		|	ВТ_БезПроблемыКопеек.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВТ_БезПроблемыКопеек.ВариантОбеспечения КАК ВариантОбеспечения,
		|	ВТ_БезПроблемыКопеек.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ВТ_БезПроблемыКопеек.Упаковка КАК Упаковка,
		|	ВТ_БезПроблемыКопеек.Цена КАК Цена,
		|	ВТ_БезПроблемыКопеек.Склад КАК Склад,
		|	ВТ_БезПроблемыКопеек.ЕТС_КоммерческоеПредложение КАК ЕТС_КоммерческоеПредложение,
		|	ВТ_БезПроблемыКопеек.СтавкаНДС КАК СтавкаНДС,
		|	ВТ_БезПроблемыКопеек.Сумма КАК Сумма,
		|	ВТ_БезПроблемыКопеек.СуммаНДС КАК СуммаНДС,
		|	ВТ_БезПроблемыКопеек.СуммаСНДС КАК СуммаСНДС,
		|	ВТ_БезПроблемыКопеек.Доступно КАК Доступно,
		|	КОЛИЧЕСТВО(ВТ_БезПроблемыКопеек1.Номенклатура) КАК НомерПП
		|ПОМЕСТИТЬ ВТ_НумерованныяДляЗаполненияКопеек
		|ИЗ
		|	ВТ_БезПроблемыКопеек КАК ВТ_БезПроблемыКопеек
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_БезПроблемыКопеек КАК ВТ_БезПроблемыКопеек1
		|		ПО ВТ_БезПроблемыКопеек.СуммаСНДС <= ВТ_БезПроблемыКопеек1.СуммаСНДС
		|			И ВТ_БезПроблемыКопеек.ВариантОбеспечения <= ВТ_БезПроблемыКопеек1.ВариантОбеспечения
		|			И ВТ_БезПроблемыКопеек.Номенклатура = ВТ_БезПроблемыКопеек1.Номенклатура
		|			И ВТ_БезПроблемыКопеек.Характеристика = ВТ_БезПроблемыКопеек1.Характеристика
		|			И ВТ_БезПроблемыКопеек.ВидЦены = ВТ_БезПроблемыКопеек1.ВидЦены
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_БезПроблемыКопеек.Номенклатура,
		|	ВТ_БезПроблемыКопеек.Характеристика,
		|	ВТ_БезПроблемыКопеек.ВидЦены,
		|	ВТ_БезПроблемыКопеек.ЕТС_Поставщик,
		|	ВТ_БезПроблемыКопеек.Количество,
		|	ВТ_БезПроблемыКопеек.КоличествоУпаковок,
		|	ВТ_БезПроблемыКопеек.ВариантОбеспечения,
		|	ВТ_БезПроблемыКопеек.ДатаОтгрузки,
		|	ВТ_БезПроблемыКопеек.Упаковка,
		|	ВТ_БезПроблемыКопеек.Цена,
		|	ВТ_БезПроблемыКопеек.Склад,
		|	ВТ_БезПроблемыКопеек.ЕТС_КоммерческоеПредложение,
		|	ВТ_БезПроблемыКопеек.СтавкаНДС,
		|	ВТ_БезПроблемыКопеек.Сумма,
		|	ВТ_БезПроблемыКопеек.СуммаНДС,
		|	ВТ_БезПроблемыКопеек.СуммаСНДС,
		|	ВТ_БезПроблемыКопеек.Доступно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НумерованныяДляЗаполненияКопеек.Номенклатура КАК Номенклатура,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Характеристика КАК Характеристика,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ВидЦены КАК ВидЦены,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_Поставщик КАК ЕТС_Поставщик,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_Поставщик.Наименование КАК Поставщик,
		|	ВЫБОР
		|		КОГДА ВТ_НумерованныяДляЗаполненияКопеек.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Обособленно,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Количество КАК Количество,
		|	ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ВариантОбеспечения КАК ВариантОбеспечения,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Упаковка КАК Упаковка,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Цена КАК Цена,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Склад КАК Склад,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_КоммерческоеПредложение КАК ЕТС_КоммерческоеПредложение,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СтавкаНДС КАК СтавкаНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Сумма + ЕСТЬNULL(ВТ_КопейкиРазница.СуммаРазница, 0) КАК Сумма,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СуммаНДС + ЕСТЬNULL(ВТ_КопейкиРазница.СуммаНДСРазница, 0) КАК СуммаНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СуммаСНДС + ЕСТЬNULL(ВТ_КопейкиРазница.СуммаСНДСРазница, 0) КАК СуммаСНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Доступно КАК Доступно,
		|	ВЫБОР
		|		КОГДА ВТ_НумерованныяДляЗаполненияКопеек.Доступно >= ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок
		|			ТОГДА ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок
		|		ИНАЧЕ ВТ_НумерованныяДляЗаполненияКопеек.Доступно
		|	КОНЕЦ КАК Резерв,
		|	ВТ_НумерованныяДляЗаполненияКопеек.НомерПП КАК НомерПП
		|ИЗ
		|	ВТ_НумерованныяДляЗаполненияКопеек КАК ВТ_НумерованныяДляЗаполненияКопеек
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КопейкиРазница КАК ВТ_КопейкиРазница
		|		ПО ВТ_НумерованныяДляЗаполненияКопеек.Номенклатура = ВТ_КопейкиРазница.Номенклатура
		|			И ВТ_НумерованныяДляЗаполненияКопеек.Характеристика = ВТ_КопейкиРазница.Характеристика
		|			И ВТ_НумерованныяДляЗаполненияКопеек.ВидЦены = ВТ_КопейкиРазница.ВидЦены
		|ГДЕ
		|	ВТ_НумерованныяДляЗаполненияКопеек.НомерПП <= 1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_НумерованныяДляЗаполненияКопеек.Номенклатура,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Характеристика,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ВидЦены,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_Поставщик,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_Поставщик.Наименование,
		|	ВЫБОР
		|		КОГДА ВТ_НумерованныяДляЗаполненияКопеек.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Количество,
		|	ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ВариантОбеспечения,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ДатаОтгрузки,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Упаковка,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Цена,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Склад,
		|	ВТ_НумерованныяДляЗаполненияКопеек.ЕТС_КоммерческоеПредложение,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СтавкаНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Сумма,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СуммаНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.СуммаСНДС,
		|	ВТ_НумерованныяДляЗаполненияКопеек.Доступно,
		|	ВЫБОР
		|		КОГДА ВТ_НумерованныяДляЗаполненияКопеек.Доступно >= ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок
		|			ТОГДА ВТ_НумерованныяДляЗаполненияКопеек.КоличествоУпаковок
		|		ИНАЧЕ ВТ_НумерованныяДляЗаполненияКопеек.Доступно
		|	КОНЕЦ,
		|	ВТ_НумерованныяДляЗаполненияКопеек.НомерПП
		|ИЗ
		|	ВТ_НумерованныяДляЗаполненияКопеек КАК ВТ_НумерованныяДляЗаполненияКопеек
		|ГДЕ
		|	ВТ_НумерованныяДляЗаполненияКопеек.НомерПП > 1
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	ВариантОбеспечения УБЫВ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ВернутьТЗ Тогда
		Возврат Результат;
	Иначе 	
		Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Функция ПолучитьРеквизитыСсылки(Ссылка,Параметры) Экспорт

	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,Параметры);	

КонецФункции // ПолучитьРеквизитыСсылки()

Процедура СоздатьДокументыПотребностейНаОснованииЗаказаКлиента(Ссылка) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК ДокументОснование,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.Количество КАК КоличествоУпаковок,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК НачалоОтгрузки,
		|	ЗаказКлиентаТовары.ЕТС_Поставщик КАК СкладОтправитель,
		|	ЗаказКлиентаТовары.ЕТС_Поставщик КАК Партнер,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить) КАК ВариантОбеспечения,
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.Склад КАК Склад,
		|	ЗаказКлиента.Склад КАК СкладПолучатель,
		|	ЗаказКлиента.Подразделение КАК Подразделение,
		|	ЗаказКлиентаТовары.ВидЦены.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера) КАК ЭтоЗаказПоставщику,
		|	ЗаказКлиентаТовары.Цена КАК Цена,
		|	ЗаказКлиентаТовары.Сумма КАК Сумма,
		|	ЗаказКлиентаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ЗаказКлиентаТовары.СуммаНДС КАК СуммаНДС,
		|	ЗаказКлиентаТовары.СуммаСНДС КАК СуммаСНДС,
		|	ЗаказКлиентаТовары.ЕТС_АвиаПеревозка КАК ЕТС_АвиаПеревозка,
		|	ЗаказКлиента.Назначение КАК Назначение
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО ЗаказКлиентаТовары.Ссылка = ЗаказКлиента.Ссылка
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка
		|	И ЗаказКлиентаТовары.Ссылка = &Ссылка
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|	И ЗаказКлиентаТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления)
		|	И ЗаказКлиентаТовары.ВидЦены.Назначение <> ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|ИТОГИ
		|	МАКСИМУМ(ДокументОснование),
		|	МАКСИМУМ(Организация),
		|	МАКСИМУМ(Склад),
		|	МАКСИМУМ(СкладПолучатель),
		|	МАКСИМУМ(Подразделение),
		|	МАКСИМУМ(ЭтоЗаказПоставщику)
		|ПО
		|	СкладОтправитель";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСкладОтправителя = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСкладОтправителя.Следующий() Цикл
		Если ВыборкаСкладОтправителя.ЭтоЗаказПоставщику Тогда
			ДокументОбъект = Документы.ЗаказПоставщику.СоздатьДокумент();
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
			ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.НеСогласован;
			ДокументОбъект.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
			ДокументОбъект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
			ДокументОбъект.ЗакупкаПодДеятельность = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
			ДокументОбъект.Валюта = Константы.ВалютаРегламентированногоУчета.Получить();;
		Иначе 	
			ДокументОбъект = Документы.ЗаказНаПеремещение.СоздатьДокумент();
			ДокументОбъект.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
			ДокументОбъект.Статус = Перечисления.СтатусыВнутреннихЗаказов.Закрыт;
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
		КонецЕсли;
		
		ДокументОбъект.Дата = ТекущаяДата();
		ДокументОбъект.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
		ДокументОбъект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект,ВыборкаСкладОтправителя);
		
		Если ВыборкаСкладОтправителя.ЭтоЗаказПоставщику И ЗначениеЗаполнено(ВыборкаСкладОтправителя.Партнер) Тогда
			ДокументОбъект.Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("Партнер",ДокументОбъект.Партнер);		
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаСкладОтправителя.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект.Товары.Добавить(),ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
		ДокументПроведен = Ложь;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			ДокументПроведен = Истина;
		Исключение
			ДокументПроведен = Ложь;
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
		Если Не ДокументПроведен Тогда
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ЕТС_СоздатьЭлементСправочникаДопСвойстваДоговора(Ссылка) Экспорт
	
	Выборка = Справочники.ЕТС_ДоговорыДопСвойства.Выбрать(,Ссылка);
	
	Если Не Выборка.Следующий() Тогда
		ЭлементСпр = Справочники.ЕТС_ДоговорыДопСвойства.СоздатьЭлемент();
		ЭлементСпр.Владелец = Ссылка;
		ЭлементСпр.Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"Наименование");
		ЭлементСпр.Записать();
		ВозвращаемоеЗначение = ЭлементСпр.Ссылка;
	Иначе 
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Для справочника %1 уже созданы доп. свойства!",Ссылка));
		ВозвращаемоеЗначение = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;

КонецФункции

Функция ЕТС_СоздатьСчетНаОснованииЗаказаКлиента(Ссылка) Экспорт
	
	ШапкаОснование = Новый Структура("
		|Партнер,
		|Контрагент,
		|Договор,
		|Организация,
		|Валюта,
		|ДокументОснование,
		|СуммаДокумента,
		|НомерДокумента,
		|БанковскийСчет,
		|Префикс,
		|Касса,
		|ФормаОплаты, 
		|КонтактноеЛицо,
		|Руководитель,
		|ГлавныйБухгалтер
		|");
	
	ТаблицаЭтапов = Новый ТаблицаЗначений;
	ТаблицаЭтапов.Колонки.Добавить("СуммаПлатежа");
	ТаблицаЭтапов.Колонки.Добавить("ПроцентПлатежа");
	ТаблицаЭтапов.Колонки.Добавить("ДатаПлатежа");
	ТаблицаЭтапов.Колонки.Добавить("Выбран");
	ТаблицаЭтапов.Колонки.Добавить("ИндексКартинки");
	ТаблицаЭтапов.Колонки.Добавить("СуммаОплаты");
	ТаблицаЭтапов.Колонки.Добавить("Оплачен",Новый ОписаниеТипов("Булево"));
	ТаблицаЭтапов.Колонки.Добавить("СуммаКОплате");
	ТаблицаЭтапов.Колонки.Добавить("ЭтоЗалогЗаТару",Новый ОписаниеТипов("Булево"));
	ТаблицаЭтапов.Колонки.Добавить("ЭтапСверхЗаказа");
	
	ДокументОснование = ПродажиВызовСервера.ПолучитьОснованиеДляСчетаНаОплату(Ссылка);
	
	СчетНаОплатуКлиенту = Неопределено;
	
	//Текст из формы счета
	//ФормаСозданияСчетовНаОплату
	//Процедура ОбновитьСервер()
	
	Если ТипЗнч(ДокументОснование) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                    КАК Договор,
		|	ДанныеДокумента.Партнер                   КАК Партнер,
		|	ДанныеДокумента.Контрагент                КАК Контрагент,
		|	ДанныеДокумента.Организация               КАК Организация,
		|	ДанныеДокумента.ВалютаВзаиморасчетов      КАК Валюта,
		|	ДанныеДокумента.Ссылка                    КАК ДокументОснование,
		|	0                                         КАК СуммаДокумента,
		|	ДанныеДокумента.Номер                     КАК НомерДокумента,
		|	ДанныеДокумента.БанковскийСчет            КАК БанковскийСчет,
		|	ДанныеДокумента.Организация.Префикс       КАК Префикс,
		|	Неопределено                              КАК Касса,
		|	Неопределено                              КАК ФормаОплаты,
		|	ДанныеДокумента.Ссылка                    КАК Документ,
		|	ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Статус                    КАК Статус,
		|	ДанныеДокумента.КонтактноеЛицо            КАК КонтактноеЛицо,
		|
		|	ВЫБОР КОГДА ДанныеДокумента.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован) ТОГДА
		|		ИСТИНА
		|	ИНАЧЕ
		|		ЛОЖЬ
		|	КОНЕЦ                                     КАК ЕстьОшибкиСтатус
		|
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РасчетыСКлиентами.Период, ДЕНЬ) КАК Период,
		|	СУММА(РасчетыСКлиентами.КОплате) КАК КОплате
		|ПОМЕСТИТЬ ТаблицаРасчеты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|ГДЕ
		|	РасчетыСКлиентами.ОбъектРасчетов.Объект = &Договор
		|	И РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И РасчетыСКлиентами.КОплате > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЛОЖЬ                                                КАК Выбран,
		|	ЛОЖЬ                                                КАК Оплачена,
		|	1                                                   КАК ИндексКартинки,
		|	МАКСИМУМ(ТаблицаПериодов.КОплате)                   КАК СуммаПлатежа,
		|
		|	ВЫБОР КОГДА МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|			>= СУММА(ТаблицаКОплате.КОплате) ТОГДА
		|		МАКСИМУМ(ТаблицаПериодов.КОплате)
		|	ИНАЧЕ
		|		МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|			- (СУММА(ТаблицаКОплате.КОплате) - МАКСИМУМ(ТаблицаПериодов.КОплате))
		|	КОНЕЦ                                               КАК СуммаКОплате,
		|
		|	ТаблицаПериодов.Период                              КАК ДатаПлатежа,
		|
		|	ВЫБОР КОГДА МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|			>= СУММА(ТаблицаКОплате.КОплате) ТОГДА
		|		МАКСИМУМ(ТаблицаПериодов.КОплате)
		|	ИНАЧЕ
		|		МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|			- (СУММА(ТаблицаКОплате.КОплате) - МАКСИМУМ(ТаблицаПериодов.КОплате))
		|	КОНЕЦ / МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|	* 100                                               КАК ПроцентПлатежа,
		|
		|	МАКСИМУМ(ТаблицаПериодов.КОплате) - ВЫБОР КОГДА МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток
		|			- РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток) >= СУММА(ТаблицаКОплате.КОплате) ТОГДА
		|		МАКСИМУМ(ТаблицаПериодов.КОплате)
		|	ИНАЧЕ
		|		МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|			- (СУММА(ТаблицаКОплате.КОплате) - МАКСИМУМ(ТаблицаПериодов.КОплате))
		|	КОНЕЦ                                               КАК СуммаОплаты
		|
		|ИЗ
		|	ТаблицаРасчеты КАК ТаблицаПериодов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаРасчеты КАК ТаблицаКОплате
		|		ПО ТаблицаПериодов.Период <= ТаблицаКОплате.Период
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(, ОбъектРасчетов.Объект = &Договор) КАК РасчетыСКлиентамиОстатки
		|		ПО ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПериодов.Период
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(РасчетыСКлиентамиОстатки.КОплатеОстаток - РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток)
		|		> СУММА(ТаблицаКОплате.КОплате) - МАКСИМУМ(ТаблицаПериодов.КОплате)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаПериодов.Период
		|");
		Запрос.УстановитьПараметр("Договор", ДокументОснование);
		
		МассивРезультатов       = Запрос.ВыполнитьПакет();
		
		Если МассивРезультатов[2].Пустой() Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не требуется вводить счета на оплату. Остаток задолженности по договору %1 равен 0.'"),
				ДокументОснование);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		ВыборкаШапка            = МассивРезультатов[0].Выбрать();
		// МассивРезультатов[1] - ТаблицаРасчеты
		ВыборкаЭтаповОплаты     = МассивРезультатов[2].Выбрать();
		
		Если ВыборкаШапка.Следующий() Тогда
			
			ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
				ВыборкаШапка.ДокументОснование,
				ВыборкаШапка.Статус,
				, // ЕстьОшибкиПроведен
				ВыборкаШапка.ЕстьОшибкиСтатус);
			
			ЗаполнитьЗначенияСвойств(ШапкаОснование, ВыборкаШапка);
			Валюта = ВыборкаШапка.Валюта;
			
		КонецЕсли;
		
		ТаблицаЭтапов.Очистить();
		
		Пока ВыборкаЭтаповОплаты.Следующий() Цикл
			
			НоваяСтрокаЭтап = ТаблицаЭтапов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЭтап, ВыборкаЭтаповОплаты);
			
		КонецЦикла;
			
		ИтогоСуммаКОплате    = ТаблицаЭтапов.Итог("СуммаКОплате");
		ИтогоСуммаОплаты     = ТаблицаЭтапов.Итог("СуммаОплаты");
		ИтогоСуммаПлатежа    = ТаблицаЭтапов.Итог("СуммаПлатежа");
		ИтогоПроцентПлатежа  = ТаблицаЭтапов.Итог("ПроцентПлатежа");
		
		ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
		
		ИтогоОтмеченоКОплате = 0;
		
		Для Каждого ТекущийЭтап Из ТаблицаЭтапов Цикл
			
			Если Не ТекущийЭтап.Оплачен Тогда
				Если ТекущийЭтап.ДатаПлатежа <= ТекущаяДата Тогда
					ТекущийЭтап.Выбран = Истина;
					ИтогоОтмеченоКОплате = ИтогоОтмеченоКОплате + ТекущийЭтап.СуммаКОплате;
				КонецЕсли;
				
				Если ТекущийЭтап.ДатаПлатежа > ТекущаяДата Тогда
					ТекущийЭтап.Выбран = Истина;
					ИтогоОтмеченоКОплате = ИтогоОтмеченоКОплате + ТекущийЭтап.СуммаКОплате;
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ТекстЗапросаДокумент = ДокументОснование.Метаданные().ПолноеИмя();
		
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	ЭтапыГрафикаОплаты.Ссылка                           КАК Документ,
			|	1                                                   КАК Порядок,
			|	ЛОЖЬ                                                КАК Выбран,
			|	ЛОЖЬ                                                КАК Оплачена,
			|	1                                                   КАК ИндексКартинки,
			|	ЭтапыГрафикаОплаты.СуммаПлатежа                     КАК СуммаПлатежа,
			|	ЭтапыГрафикаОплаты.СуммаПлатежа                     КАК СуммаКОплате,
			|	ЭтапыГрафикаОплаты.ДатаПлатежа                      КАК ДатаПлатежа,
			|	ЭтапыГрафикаОплаты.ПроцентПлатежа                   КАК ПроцентПлатежа,
			|	ЛОЖЬ                                                КАК ЭтоЗалогЗаТару,
			|	ЛОЖЬ                                                КАК ЭтапСверхЗаказа
			|ИЗ
			|	#ДокументЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
			|ГДЕ
			|	ЭтапыГрафикаОплаты.Ссылка  = &ДокументОснование
			|	И ЭтапыГрафикаОплаты.СуммаПлатежа <> 0
			|");
		
		Если ТекстЗапросаДокумент = "Документ.ЗаказКлиента" ИЛИ ТекстЗапросаДокумент = "Документ.ЗаявкаНаВозвратТоваровОтКлиента" Тогда
			Запрос.Текст = Запрос.Текст + ТекстЗаказКлиента();
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + ТекстУпорядочить();
		Запрос.Текст = Запрос.Текст + ТекстЗапросаРасчетов();
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДанныеДокумента.Партнер                   КАК Партнер,
		|	ДанныеДокумента.Контрагент                КАК Контрагент,
		|	ДанныеДокумента.Договор		              КАК Договор,
		|	ДанныеДокумента.Организация               КАК Организация,
		|	ДанныеДокумента.Руководитель              КАК Руководитель,
		|	ДанныеДокумента.ГлавныйБухгалтер          КАК ГлавныйБухгалтер,
		|	ДанныеДокумента.Валюта                    КАК Валюта,
		|	ДанныеДокумента.Ссылка                    КАК ДокументОснование,
		|	ДанныеДокумента.Номер                     КАК НомерДокумента,
		|	ДанныеДокумента.БанковскийСчет            КАК БанковскийСчет,
		|	ДанныеДокумента.Организация.Префикс       КАК Префикс,
		|	ДанныеДокумента.Касса                     КАК Касса,
		|	&ДанныеДокументаКонтактноеЛицо            КАК КонтактноеЛицо,
		|	&ДанныеДокументаЭтоЗаказКакСчет           КАК ЭтоЗаказКакСчет,
		|	ДанныеДокумента.ФормаОплаты               КАК ФормаОплаты,
		|	ДанныеДокумента.Ссылка                    КАК Документ,
		|	
		|	&Статус                                   КАК Статус,
		|	&ЕстьОшибкиХозяйственнаяОперация          КАК ЕстьОшибкиХозяйственнаяОперация,
		|	&ХозяйственнаяОперация                    КАК ХозяйственнаяОперация,
		|	
		|	НЕ ДанныеДокумента.Проведен               КАК ЕстьОшибкиПроведен,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.Ссылка) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
		|			ТОГДА ВЫБОР
		|					КОГДА &ДанныеДокументаТребуетсяЗалогЗаТару
		|						ТОГДА &ДанныеДокументаСуммаЗамены + &ДанныеДокументаСуммаВозвратнойТары
		|					ИНАЧЕ &ДанныеДокументаСуммаЗамены
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ТИПЗНАЧЕНИЯ(ДанныеДокумента.Ссылка) = ТИП(Документ.ЗаказКлиента)
		|					ТОГДА ВЫБОР
		|							КОГДА &ДанныеДокументаТребуетсяЗалогЗаТару
		|								ТОГДА &ДанныеДокументаСуммаДокумента + &ДанныеДокументаСуммаВозвратнойТары
		|							ИНАЧЕ &ДанныеДокументаСуммаДокумента
		|						КОНЕЦ
		|				ИНАЧЕ &ДанныеДокументаСуммаДокумента
		|			КОНЕЦ
		|	КОНЕЦ                                     КАК СуммаДокумента
		|ИЗ
		|	#ТекстЗапросаДокумент КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &ДокументОснование
		|"; 
		
		Запрос.Текст = Запрос.Текст + ТекстЗапроса;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументЭтапыГрафикаОплаты", ТекстЗапросаДокумент + ".ЭтапыГрафикаОплаты");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТекстЗапросаДокумент", ТекстЗапросаДокумент);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаСуммаДокумента",       "ДанныеДокумента.СуммаДокумента");
		
		Если ТекстЗапросаДокумент = "Документ.ЗаявкаНаВозвратТоваровОтКлиента" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаСуммаЗамены", "ДанныеДокумента.СуммаЗамены");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаСуммаЗамены", "0");
		КонецЕсли;
		
		Если ТекстЗапросаДокумент = "Документ.ЗаказКлиента" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаКонтактноеЛицо", "ДанныеДокумента.КонтактноеЛицо");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаЭтоЗаказКакСчет", "ДанныеДокумента.ЭтоЗаказКакСчет");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаКонтактноеЛицо", """");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаЭтоЗаказКакСчет", """");
		КонецЕсли;
		
		Если ТекстЗапросаДокумент = "Документ.ЗаявкаНаВозвратТоваровОтКлиента" ИЛИ ТекстЗапросаДокумент = "Документ.ЗаказКлиента" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаСуммаВозвратнойТары",  "ДанныеДокумента.СуммаВозвратнойТары");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаТребуетсяЗалогЗаТару", "ДанныеДокумента.ТребуетсяЗалогЗаТару");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаСуммаВозвратнойТары", "0");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДанныеДокументаТребуетсяЗалогЗаТару", "ЛОЖЬ");
		КонецЕсли;
		
		Если ТекстЗапросаДокумент <> "Документ.ОтчетКомиссионера" И ТекстЗапросаДокумент <> "Документ.ОтчетКомитентуОЗакупках" Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Статус", "ДанныеДокумента.Статус");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЕстьОшибкиХозяйственнаяОперация", ПолеЕстьОшибкиХозяйственнаяОперация());
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ХозяйственнаяОперация", "ДанныеДокумента.ХозяйственнаяОперация");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Статус", "НЕОПРЕДЕЛЕНО");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЕстьОшибкиХозяйственнаяОперация", "ЛОЖЬ");
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ХозяйственнаяОперация", "НЕОПРЕДЕЛЕНО");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента", ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
		МассивРезультатов            = Запрос.ВыполнитьПакет();
		ТаблицаДокументовЭтапы       = МассивРезультатов[0].Выгрузить();
		ВыборкаДокументовОплата      = МассивРезультатов[1].Выбрать();
		ВыборкаШапка                 = МассивРезультатов[2].Выбрать();
		
		Если ВыборкаШапка.Следующий() Тогда
			
			МассивДопустимыхСтатусов = Неопределено;
			ТипОснования = ТипЗнч(ДокументОснование);
			
			Если ТипОснования = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				
				Документы.ЗаказКлиента.ПроверитьВозможностьВводаНаОсновании(
					ВыборкаШапка.ДокументОснование,
					ВыборкаШапка.Статус,
					ВыборкаШапка.ЕстьОшибкиПроведен,
					Истина);

			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
				
				Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПроверитьВозможностьВводаНаОсновании(
					ВыборкаШапка.ДокументОснование,
					ВыборкаШапка.Статус,
					ВыборкаШапка.ЕстьОшибкиПроведен,
					Истина);
				
			КонецЕсли;
			
			Если ТекстЗапросаДокумент <> "Документ.ОтчетКомиссионера" Тогда
				
				Документы.СчетНаОплатуКлиенту.ПроверитьКорректностьХозяйственнойОперацииДокументаОснования(
					ВыборкаШапка.ЕстьОшибкиХозяйственнаяОперация,
					ВыборкаШапка.ХозяйственнаяОперация);
				
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ШапкаОснование, ВыборкаШапка);
			Валюта = ВыборкаШапка.Валюта;
			
		КонецЕсли;
		
		Если ТаблицаДокументовЭтапы.Количество() = 0 Тогда
			
			ТекстОшибки = НСтр("ru='В документе %Документ% не заполнены этапы графика оплаты'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДокументОснование);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		ТаблицаЭтапов.Очистить();
		
		Для Каждого СтрокаДокументаЭтап Из ТаблицаДокументовЭтапы Цикл
			
			Если СтрокаДокументаЭтап.СуммаКОплате = 0 И СтрокаДокументаЭтап.СуммаПлатежа = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаЭтап = ТаблицаЭтапов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаЭтап, СтрокаДокументаЭтап);
			
		КонецЦикла;
		
		Если ТаблицаЭтапов.Итог("СуммаПлатежа") = 0 Тогда
			
			ТекстОшибки = НСтр("ru='Не требуется вводить счета на оплату. Сумма платежа по документу %Документ% равна 0.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДокументОснование);
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Если ВыборкаДокументовОплата.Следующий() Тогда
			
			СуммаОстатокОплаты = 0;
			СуммаОстатокОплаты = ВыборкаДокументовОплата.СуммаОплаты;
			
			Для Каждого ТекущийЭтап Из ТаблицаЭтапов Цикл
				
				Если СуммаОстатокОплаты < ТекущийЭтап.СуммаПлатежа Тогда
					ТекущийЭтап.СуммаОплаты = СуммаОстатокОплаты;
					ТекущийЭтап.СуммаКОплате = ТекущийЭтап.СуммаПлатежа - ТекущийЭтап.СуммаОплаты;
					Прервать;
				КонецЕсли;
					
				ТекущийЭтап.СуммаОплаты  = ТекущийЭтап.СуммаПлатежа;
				ТекущийЭтап.СуммаКОплате = 0;
				ТекущийЭтап.Оплачен      = Истина;
				СуммаОстатокОплаты       = СуммаОстатокОплаты - ТекущийЭтап.СуммаПлатежа;
				
			КонецЦикла;
				
		КонецЕсли;
		
		ИтогоСуммаКОплате    = ТаблицаЭтапов.Итог("СуммаКОплате");
		ИтогоСуммаОплаты     = ТаблицаЭтапов.Итог("СуммаОплаты");
		ИтогоСуммаПлатежа    = ТаблицаЭтапов.Итог("СуммаПлатежа");
		ИтогоПроцентПлатежа  = ТаблицаЭтапов.Итог("ПроцентПлатежа");
		
		ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
		
		ИтогоОтмеченоКОплате = 0;
		
		Для Каждого ТекущийЭтап Из ТаблицаЭтапов Цикл
			
			Если Не ТекущийЭтап.Оплачен Тогда
				Если ТекущийЭтап.ДатаПлатежа <= ТекущаяДата Тогда
					ТекущийЭтап.Выбран = Истина;
					ИтогоОтмеченоКОплате = ИтогоОтмеченоКОплате + ТекущийЭтап.СуммаКОплате;
				КонецЕсли;
				
				Если ТекущийЭтап.ДатаПлатежа > ТекущаяДата Тогда
					ТекущийЭтап.Выбран = Истина;
					ИтогоОтмеченоКОплате = ИтогоОтмеченоКОплате + ТекущийЭтап.СуммаКОплате;
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	//Текст из формы счета
	//ФормаСозданияСчетовНаОплату
	//Функция СоздатьСчетНаОплатуСервер()
	
	
	СчетНаОплатуКлиенту = Неопределено;
	
	НачатьТранзакцию();
	
	Попытка
		
		ДокументОбъект = Документы.СчетНаОплатуКлиенту.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ШапкаОснование);
		ДокументОбъект.Заполнить(Неопределено);
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.УстановитьНовыйНомер();
		
		СуммаДокумента   = 0;
		КоличествоЭтапов = 0;
		НомерЭтапа       = 1;
		ПроцентПлатежа   = 0;
		СуммаДокументаБезЗалога = 0;
		
		Для Каждого ТекущийЭтап Из ТаблицаЭтапов Цикл
			
			Если Не ТекущийЭтап.Выбран Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаДокумента = СуммаДокумента + ТекущийЭтап.СуммаПлатежа - ТекущийЭтап.СуммаОплаты;
			Если Не ТекущийЭтап.ЭтоЗалогЗаТару Тогда
				СуммаДокументаБезЗалога = СуммаДокументаБезЗалога + ТекущийЭтап.СуммаПлатежа - ТекущийЭтап.СуммаОплаты;
			КонецЕсли;
			КоличествоЭтапов = КоличествоЭтапов + 1;
			
		КонецЦикла;
		
		ДокументОбъект.СуммаДокумента = СуммаДокумента;
		
		Если ШапкаОснование.СуммаДокумента <> СуммаДокумента Тогда
			ДокументОбъект.ЧастичнаяОплата = Истина;
		КонецЕсли;
		
		Для Каждого ТекущийЭтап Из ТаблицаЭтапов Цикл
			
			Если Не ТекущийЭтап.Выбран Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйЭтап = ДокументОбъект.ЭтапыГрафикаОплаты.Добавить();
			НовыйЭтап.ДатаПлатежа    = ТекущийЭтап.ДатаПлатежа;
			НовыйЭтап.СуммаПлатежа   = ТекущийЭтап.СуммаКОплате;
			НовыйЭтап.ЭтоЗалогЗаТару = ТекущийЭтап.ЭтоЗалогЗаТару;
			
			Если СуммаДокументаБезЗалога > 0 Тогда
				Если НомерЭтапа = КоличествоЭтапов Тогда
					НовыйЭтап.ПроцентПлатежа = 100 - ПроцентПлатежа;
				ИначеЕсли Не ТекущийЭтап.ЭтоЗалогЗаТару Тогда
					НовыйЭтап.ПроцентПлатежа = НовыйЭтап.СуммаПлатежа * 100 / СуммаДокументаБезЗалога;
					ПроцентПлатежа = ПроцентПлатежа + НовыйЭтап.ПроцентПлатежа;
				КонецЕсли;
			КонецЕсли;
			
			НомерЭтапа = НомерЭтапа + 1;
			
		КонецЦикла;
		
		СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
		СтруктураПараметров.Организация    		= ДокументОбъект.Организация;
		СтруктураПараметров.БанковскийСчет		= ДокументОбъект.БанковскийСчет;
		БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
		
		СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияКассыОрганизацииПоУмолчанию();
		СтруктураПараметров.Организация = ДокументОбъект.Организация;
		СтруктураПараметров.ФормаОплаты	= ДокументОбъект.ФормаОплаты;
		СтруктураПараметров.Касса		= ДокументОбъект.Касса;
		Касса          = ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию(СтруктураПараметров);
		
		ДокументОбъект.НазначениеПлатежа = Документы.СчетНаОплатуКлиенту.СформироватьНазначениеПлатежа(
			ШапкаОснование.НомерДокумента,
			ШапкаОснование.ДокументОснование);
		ДокументОбъект.ДокументОснование = Ссылка;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		СчетНаОплатуКлиенту = ДокументОбъект.Ссылка;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстОшибки = НСтр("ru='Не удалось записать %Документ%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%",       ДокументОбъект);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
	КонецПопытки;
	
	Возврат СчетНаОплатуКлиенту;
	
КонецФункции

Функция ТекстУпорядочить()
	
	Текст = "УПОРЯДОЧИТЬ ПО
	|	ДатаПлатежа,
	|	Порядок
	|;";
	
	Возврат Текст;
	
КонецФункции

Функция ТекстЗапросаРасчетов()
	
	ТекстЗапросаРасчетов = "";
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		ТекстЗапросаРасчетов = "
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(&ШаблонПоляОплачено), 0) КАК СуммаОплаты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
		|ГДЕ
		|	РасчетыПоСрокам.ОбъектРасчетов.Объект = &ДокументОснование
		|	И РасчетыПоСрокам.Активность 
		|;";
		ТекстЗапросаРасчетов = СтрЗаменить(ТекстЗапросаРасчетов, 
		"&ШаблонПоляОплачено", 
		СтрШаблон("%1 + %2", 
		ВзаиморасчетыСервер.ШаблонПоляОплаченоКлиентом(),
		ВзаиморасчетыСервер.ШаблонПоляЗачтеноКлиенту()));
		ТекстЗапросаРасчетов = СтрЗаменить(ТекстЗапросаРасчетов, "&ДанныеОтчета", "4");
	Иначе
		ТекстЗапросаРасчетов = "
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ВЫБОР 
		|			КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				И (НЕ РасчетыСКлиентами.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса),
		|				                                                 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту),
		|				                                                 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
		|				                                                 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВРозницу))
		|					ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности))
		|				ТОГДА РасчетыСКлиентами.КОплате
		|			ИНАЧЕ 0
		|		КОНЕЦ +
		|		ВЫБОР
		|			КОГДА РасчетыСКлиентами.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
		|				ТОГДА ВЫБОР
		|						КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|							ТОГДА РасчетыСКлиентами.Оплачивается
		|						ИНАЧЕ - РасчетыСКлиентами.Оплачивается
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ), 0) КАК СуммаОплаты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|ГДЕ
		|	РасчетыСКлиентами.ОбъектРасчетов.Объект = &ДокументОснование
		|	И РасчетыСКлиентами.Активность 
		|;";
	КонецЕсли;
	
	Возврат ТекстЗапросаРасчетов;
	
КонецФункции

Функция ТекстЗаказКлиента()
	
	ТекстЗапроса = "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЭтапыГрафикаОплаты.Ссылка                           КАК Документ,
	|	2                                                   КАК Порядок,
	|	ЛОЖЬ                                                КАК Выбран,
	|	ЛОЖЬ                                                КАК Оплачена,
	|	1                                                   КАК ИндексКартинки,
	|	ЭтапыГрафикаОплаты.СуммаЗалогаЗаТару                КАК СуммаПлатежа,
	|	ЭтапыГрафикаОплаты.СуммаЗалогаЗаТару                КАК СуммаКОплате,
	|	ЭтапыГрафикаОплаты.ДатаПлатежа                      КАК ДатаПлатежа,
	|	ЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару              КАК ПроцентПлатежа,
	|	ИСТИНА                                              КАК ЭтоЗалогЗаТару,
	|	ЛОЖЬ                                                КАК ЭтапСверхЗаказа
	|ИЗ
	|	#ДокументЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
	|ГДЕ
	|	ЭтапыГрафикаОплаты.Ссылка  = &ДокументОснование
	|	И ЭтапыГрафикаОплаты.Ссылка.ТребуетсяЗалогЗаТару
	|	И ЭтапыГрафикаОплаты.СуммаЗалогаЗаТару <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыСКлиентами.Регистратор КАК Документ,
	|	3 КАК Порядок,
	|	ЛОЖЬ КАК Выбран,
	|	ЛОЖЬ КАК Оплачена,
	|	1 КАК ИндексКартинки,
	|	СУММА(ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА РасчетыСКлиентами.КОплате
	|						ИНАЧЕ -РасчетыСКлиентами.КОплате
	|				КОНЕЦ) КАК СуммаПлатежа,
	|	СУММА(ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА РасчетыСКлиентами.КОплате
	|						ИНАЧЕ -РасчетыСКлиентами.КОплате
	|				КОНЕЦ) КАК СуммаКОплате,
	|	МИНИМУМ(РасчетыСКлиентами.Период) КАК ДатаПлатежа,
	|	0 КАК ПроцентПлатежа,
	|	ЛОЖЬ КАК ЭтоЗалогЗаТару,
	|	ИСТИНА КАК ЭтапСверхЗаказа
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.ОбъектРасчетов.Объект = &ДокументОснование
	|	И РасчетыСКлиентами.Регистратор <> РасчетыСКлиентами.ОбъектРасчетов.Объект
	|	И РасчетыСКлиентами.КОплате > 0 
	|	И РасчетыСКлиентами.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|	И РасчетыСКлиентами.Активность
	|	И &ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА РасчетыСКлиентами.КОплате
	|						ИНАЧЕ -РасчетыСКлиентами.КОплате
	|			КОНЕЦ) > 0
	|
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолеЕстьОшибкиХозяйственнаяОперация()
	
	ПолеЕстьОшибки = "
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ИЛИ ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ
	|";
	
	Возврат ПолеЕстьОшибки;
	
КонецФункции

Процедура ЗаполнитьВариантОбеспеченияЗаказыКлиентов(МассивЗаказовКлиента) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Обособленно КАК Обособленно,
		|	ЗаказКлиентаТовары.Ссылка.Назначение КАК Назначение,
		|	ЗаказКлиентаТовары.Склад КАК Склад,
		|	ЗаказКлиентаТовары.КоличествоУпаковок КАК Количество
		|ПОМЕСТИТЬ ДанныеТЧ
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка В(&МассивЗаказовКлиента)
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|	И НЕ ЗаказКлиентаТовары.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.НеТребуется))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеТЧ.Ссылка КАК Ссылка,
		|	ДанныеТЧ.Номенклатура КАК Номенклатура,
		|	ДанныеТЧ.Обособленно КАК Обособленно,
		|	ДанныеТЧ.Склад КАК Склад
		|ИЗ
		|	ДанныеТЧ КАК ДанныеТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(
		|				,
		|				(Номенклатура, Назначение, Склад) В
		|					(ВЫБРАТЬ
		|						ДанныеТЧ.Номенклатура КАК Номенклатура,
		|						ВЫБОР
		|							КОГДА ДанныеТЧ.Обособленно
		|								ТОГДА ДанныеТЧ.Назначение
		|							ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|						КОНЕЦ КАК Назначение,
		|						ДанныеТЧ.Склад КАК Склад
		|					ИЗ
		|						ДанныеТЧ КАК ДанныеТЧ)) КАК ЗапасыИПотребностиОстатки
		|		ПО ДанныеТЧ.Номенклатура = ЗапасыИПотребностиОстатки.Номенклатура
		|			И ДанныеТЧ.Склад = ЗапасыИПотребностиОстатки.Склад
		|			И (ВЫБОР
		|				КОГДА ДанныеТЧ.Обособленно
		|					ТОГДА ДанныеТЧ.Назначение = ЗапасыИПотребностиОстатки.Назначение
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ)
		|ГДЕ
		|	ЗапасыИПотребностиОстатки.ВНаличииОстаток >= ДанныеТЧ.Количество
		|ИТОГИ ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивЗаказовКлиента", МассивЗаказовКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСсылка.Следующий() Цикл
		ДокументОбъект = ВыборкаСсылка.Ссылка.ПолучитьОбъект();
	    СтруктураПоиска = Новый Структура("Номенклатура,Обособленно,Склад");
		
		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,ВыборкаДетальныеЗаписи);
			НайденныеСтроки = ДокументОбъект["Товары"].НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаТЧ Из НайденныеСтроки Цикл
            	СтрокаТЧ.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
				СтрокаТЧ.ДатаОтгрузки = ТекущаяДата();
			КонецЦикла;
		КонецЦикла;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось подготовить РСО!");		
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаполнитьЗаказПоставщикуИзЗаказовКлиентам(ЕТС_Партнер) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗапасыИПотребностиОстатки.Номенклатура КАК Номенклатура,
		|	ЗапасыИПотребностиОстатки.Назначение КАК Назначение,
		|	ЗапасыИПотребностиОстатки.Назначение.Заказ КАК НазначениеЗаказ,
		|	ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток КАК Резервировать
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки КАК ЗапасыИПотребностиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Остатки.Номенклатура КАК Номенклатура,
		|	ВТ_Остатки.Назначение КАК Назначение,
		|	ВТ_Остатки.НазначениеЗаказ КАК НазначениеЗаказ,
		|	ВТ_Остатки.Резервировать КАК КоличествоВЗаказе,
		|	ЗаказКлиентаТовары.ВидЦены КАК ВидЦены,
		|	ЗаказКлиентаТовары.ЕТС_АвиаПеревозка КАК ЕТС_АвиаПеревозка,
		|	ЗаказКлиентаТовары.Цена КАК Цена,
		|	ЗаказКлиентаТовары.Сумма КАК Сумма,
		|	ЗаказКлиентаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ЗаказКлиентаТовары.СуммаНДС КАК СуммаНДС,
		|	ЗаказКлиентаТовары.СуммаСНДС КАК СуммаСНДС,
		|	ЗаказКлиентаТовары.Отменено КАК Отменено,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки
		|ПОМЕСТИТЬ ВТ_Потребности
		|ИЗ
		|	ВТ_Остатки КАК ВТ_Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ПО ВТ_Остатки.НазначениеЗаказ = ЗаказКлиентаТовары.Ссылка
		|			И ВТ_Остатки.Номенклатура = ЗаказКлиентаТовары.Номенклатура
		|			И (ЗаказКлиентаТовары.Обособленно)
		|ГДЕ
		|	ЗаказКлиентаТовары.ВидЦены.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Потребности.Номенклатура КАК Номенклатура,
		|	ВТ_Потребности.Назначение КАК Назначение,
		|	ВТ_Потребности.ВидЦены КАК ЕТС_ВидЦены,
		|	ВТ_Потребности.КоличествоВЗаказе - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) КАК Количество,
		|	ВТ_Потребности.КоличествоВЗаказе - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) КАК КоличествоУпаковок,
		|	ВТ_Потребности.ВидЦены.ЕТС_Партнер КАК Контрагент,
		|	ЗаказКлиента.Склад КАК Склад,
		|	ЗаказКлиента.Подразделение КАК Подразделение,
		|	ВТ_Потребности.Цена КАК Цена,
		|	ВТ_Потребности.Сумма КАК Сумма,
		|	ВТ_Потребности.СтавкаНДС КАК СтавкаНДС,
		|	ВТ_Потребности.СуммаНДС КАК СуммаНДС,
		|	ВТ_Потребности.СуммаСНДС КАК СуммаСНДС,
		|	ВТ_Потребности.Отменено КАК Отменено,
		|	ВТ_Потребности.ДатаОтгрузки КАК ДатаОтгрузки
		|ИЗ
		|	ВТ_Потребности КАК ВТ_Потребности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(
		|				,
		|				(Номенклатура, Назначение) В
		|					(ВЫБРАТЬ
		|						ВТ_Потребности.Номенклатура КАК Номенклатура,
		|						ВТ_Потребности.Назначение КАК Назначение
		|					ИЗ
		|						ВТ_Потребности КАК ВТ_Потребности)) КАК ЗапасыИПотребностиОстатки
		|		ПО ВТ_Потребности.Номенклатура = ЗапасыИПотребностиОстатки.Номенклатура
		|			И ВТ_Потребности.Назначение = ЗапасыИПотребностиОстатки.Назначение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО ВТ_Потребности.НазначениеЗаказ = ЗаказКлиента.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО ВТ_Потребности.ВидЦены = ВидыЦен.Ссылка
		|ГДЕ
		|	ВТ_Потребности.КоличествоВЗаказе - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) > 0
		|	И ВидыЦен.ЕТС_Партнер = &ЕТС_Партнер";
	
	Запрос.УстановитьПараметр("ЕТС_Партнер", ЕТС_Партнер);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Товары = ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат);
	
	Возврат Товары;
	
КонецФункции

Процедура СоздатьЗаказыНаПеремещения(ПараметрыОтбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказКлиентаТовары.ЕТС_Поставщик КАК СкладОтправитель,
		|	ЗаказКлиентаТовары.ЕТС_АвиаПеревозка КАК ЕТС_АвиаПеревозка,
		|	&Назначение КАК Назначение
		|ПОМЕСТИТЬ Данные_ТЧ
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО ЗаказКлиентаТовары.ВидЦены = ВидыЦен.Ссылка
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|	И ЗаказКлиентаТовары.Обособленно
		|	И ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Данные_ТЧ.Номенклатура) КАК Номенклатура
		|ИЗ
		|	Данные_ТЧ КАК Данные_ТЧ
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Данные_ТЧ.Номенклатура) > 0";
	
	Запрос.УстановитьПараметр("Ссылка", ПараметрыОтбора.Ссылка);
	Запрос.УстановитьПараметр("Назначение", ПараметрыОтбора.Назначение);
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			//|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
			//|	ЗаказКлиентаТовары.Количество КАК Количество,
			//|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
			//|	ЗаказКлиентаТовары.ЕТС_Поставщик КАК СкладОтправитель,
			//|	&Назначение КАК Назначение
			//|ПОМЕСТИТЬ Данные_ТЧ
			//|ИЗ
			//|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
			//|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
			//|		ПО ЗаказКлиентаТовары.ВидЦены = ВидыЦен.Ссылка
			//|ГДЕ
			//|	ЗаказКлиентаТовары.Ссылка = &Ссылка
			//|	И ЗаказКлиентаТовары.Обособленно
			//|	И ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
			//|	И НЕ ЗаказКлиентаТовары.Отменено
			//|;
			//|
			//|////////////////////////////////////////////////////////////////////////////////
			//|ВЫБРАТЬ
			|	Данные_ТЧ.Номенклатура КАК Номенклатура,
			|	Данные_ТЧ.Назначение КАК Назначение,
			|	Данные_ТЧ.Количество - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) КАК Количество,
			|	Данные_ТЧ.Количество - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) КАК КоличествоУпаковок,
			|	Данные_ТЧ.ДатаОтгрузки КАК НачалоОтгрузки,
			|	Данные_ТЧ.СкладОтправитель КАК СкладОтправитель,
			|	Данные_ТЧ.ЕТС_АвиаПеревозка КАК ЕТС_АвиаПеревозка,
			|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить) КАК ВариантОбеспечения
			|ИЗ
			|	Данные_ТЧ КАК Данные_ТЧ
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(
			|				,
			|				Номенклатура В
			|						(ВЫБРАТЬ
			|							Данные_ТЧ.Номенклатура КАК Номенклатура
			|						ИЗ
			|							Данные_ТЧ КАК Данные_ТЧ)
			|					И Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|					И Склад = &Склад
			|					И Назначение = &Назначение) КАК ЗапасыИПотребностиОстатки
			|		ПО Данные_ТЧ.Номенклатура = ЗапасыИПотребностиОстатки.Номенклатура
			|ГДЕ
			|	Данные_ТЧ.Количество - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0) - ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) > 0
			|ИТОГИ ПО
			|	СкладОтправитель";
		
		Запрос.УстановитьПараметр("Склад", ПараметрыОтбора.Склад);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаСкладОтправителя = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСкладОтправителя.Следующий() Цикл
			
			ДокументОбъект = Документы.ЗаказНаПеремещение.СоздатьДокумент();
			ДокументОбъект.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
			ДокументОбъект.Статус = Перечисления.СтатусыВнутреннихЗаказов.Закрыт;
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
			ДокументОбъект.Дата = ТекущаяДата();
			ДокументОбъект.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
			ДокументОбъект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
			ДокументОбъект.СкладПолучатель = ПараметрыОтбора.Склад;
			ДокументОбъект.ДокументОснование = ПараметрыОтбора.Ссылка;
			ДокументОбъект.Организация = ПараметрыОтбора.Организация;
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект,ВыборкаСкладОтправителя);
			
			ВыборкаДетальныеЗаписи = ВыборкаСкладОтправителя.Выбрать();
		
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ДокументОбъект.Товары.Добавить(),ВыборкаДетальныеЗаписи);
			КонецЦикла;
			
			ДокументПроведен = Ложь;
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ДокументПроведен = Истина;
			Исключение
				ДокументПроведен = Ложь;
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
			
			Если Не ДокументПроведен Тогда
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПеремещениеТоваров_ЕТС_ЗаполнитьРезервами(ПараметрыОтбора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение КАК ЗаказНаПеремещение,
		|	ЗаказыНаПеремещениеОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыНаПеремещениеОстатки.ЗаказаноОстаток КАК КоличествоУпаковок,
		|	ЗаказыНаПеремещениеОстатки.ЗаказаноОстаток КАК Количество,
		|	ЗаказНаПеремещениеТовары.ЕТС_АвиаПеревозка КАК ЕТС_АвиаПеревозка,
		|	ЗаказНаПеремещениеТовары.КодСтроки КАК КодСтроки,
		|	ЗаказНаПеремещениеТовары.Назначение КАК Назначение
		|ИЗ
		|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, ) КАК ЗаказыНаПеремещениеОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПеремещение.Товары КАК ЗаказНаПеремещениеТовары
		|		ПО ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение = ЗаказНаПеремещениеТовары.Ссылка
		|			И ЗаказыНаПеремещениеОстатки.Номенклатура = ЗаказНаПеремещениеТовары.Номенклатура
		|ГДЕ
		|	ЗаказНаПеремещениеТовары.Ссылка.СкладОтправитель = &СкладОтправитель
		|	И ЗаказНаПеремещениеТовары.Ссылка.СкладПолучатель = &СкладПолучатель";
	
	Запрос.УстановитьПараметр("СкладОтправитель", ПараметрыОтбора.СкладОтправитель);
	Запрос.УстановитьПараметр("СкладПолучатель", ПараметрыОтбора.СкладПолучатель);

	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());

КонецФункции

Процедура ЗагрузитьССайтаЗаказКлиента() Экспорт 

		ЕТС_ОбменССайтом.ПолучитьЗаказыССайта();
	
КонецПроцедуры

Функция ПолучитьРольДоступна(Роль) Экспорт
	Возврат РольДоступна(Роль);

КонецФункции

Функция ПолучитьГрузоотправителя(Склад) Экспорт 
	
	Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад,"Подразделение");
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение,"ЕТС_Грузоотправитель");
	
КонецФункции // ПолучитьГрузоотправителя()

Функция ПолучитьОтветственноеЛицо(Организация, Подразделение) Экспорт 
	
	СтруктураОтветственных = Новый Структура("Руководитель,ОтпустилДолжность,Отпустил,ГлавныйБухгалтер");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтветственныеЛицаОрганизаций.Ссылка КАК Руководитель,
		|	ОтветственныеЛицаОрганизаций.Должность КАК ОтпустилДолжность,
		|	ОтветственныеЛицаОрганизаций.ФизическоеЛицо КАК Отпустил,
		|	0 КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ВсеОтветственныеЛица
		|ИЗ
		|	Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
		|ГДЕ
		|	ОтветственныеЛицаОрганизаций.Владелец = &Организация
		|	И ОтветственныеЛицаОрганизаций.ЕТС_Подразделение = &Подразделение
		|	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = &ОтветственноеЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтветственныеЛицаОрганизаций.Ссылка,
		|	ОтветственныеЛицаОрганизаций.Должность,
		|	ОтветственныеЛицаОрганизаций.ФизическоеЛицо,
		|	1
		|ИЗ
		|	Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
		|ГДЕ
		|	ОтветственныеЛицаОрганизаций.Владелец = &Организация
		|	И ОтветственныеЛицаОрганизаций.ЕТС_Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ОтветственныеЛицаОрганизаций.ОтветственноеЛицо = &ОтветственноеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТ_ВсеОтветственныеЛица.Руководитель КАК Руководитель,
		|	ВТ_ВсеОтветственныеЛица.ОтпустилДолжность КАК ОтпустилДолжность,
		|	ВТ_ВсеОтветственныеЛица.Отпустил КАК Отпустил
		|ИЗ
		|	ВТ_ВсеОтветственныеЛица КАК ВТ_ВсеОтветственныеЛица
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_ВсеОтветственныеЛица.Приоритет";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураОтветственных, Выборка);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СтруктураОтветственных.ГлавныйБухгалтер = Справочники.ОтветственныеЛицаОрганизаций.ПустаяСсылка();
	Иначе 	
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СтруктураОтветственных.ГлавныйБухгалтер = Выборка.Руководитель;
	КонецЕсли;
	
	Возврат СтруктураОтветственных;
	
КонецФункции // ПолучитьОтветственноеЛицо()

Процедура ПровестиДокументыЭДО(МассивДокументов) Экспорт
	
	Для Каждого Док Из МассивДокументов Цикл
		Если Не Док.Проведен Тогда
			об = Док.ПолучитьОбъект();
			об.ПометкаУдаления = Ложь;
			Попытка
				об.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьТаблицуДвиженийДляСпискаЗаказовКлиента(Ссылка) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
		|	ЗаказКлиентаТовары.НомерСтроки КАК НомерСтроки,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Склад КАК Склад,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Обособленно
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ КАК Назначение,
		|	ЗаказКлиентаТовары.ВидЦены КАК ВидЦены,
		|	СУММА(ЗаказКлиентаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	МАКСИМУМ(ЗаказКлиентаТовары.Цена) КАК Цена,
		|	СУММА(ЗаказКлиентаТовары.Сумма) КАК Сумма,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ЗаказКлиентаТовары.ВариантОбеспечения КАК ВариантОбеспечения
		|ПОМЕСТИТЬ ДанныеТЧ
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|	И НЕ ЗаказКлиентаТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Закрыт)
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Склад,
		|	ЗаказКлиентаТовары.ВидЦены,
		|	ЗаказКлиентаТовары.ДатаОтгрузки,
		|	ЗаказКлиентаТовары.Ссылка,
		|	ЗаказКлиентаТовары.НомерСтроки,
		|	ЗаказКлиентаТовары.ВариантОбеспечения,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Обособленно
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеТЧ.НомерСтроки КАК НомерСтроки,
		|	ДанныеТЧ.Номенклатура КАК Номенклатура,
		|	ДанныеТЧ.Склад КАК Склад,
		|	ДанныеТЧ.ВидЦены КАК ВидЦены,
		|	ДанныеТЧ.КоличествоУпаковок КАК Количество,
		|	ВЫБОР
		|		КОГДА ДанныеТЧ.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ЗапасыИПотребности.РезервироватьНаСкладе, 0) + ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток, 0)
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток,
		|				0) >= ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток, 0)
		|				ТОГДА ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ КАК Резерв,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) -
		|			ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток, 0) -
		|			ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток, 0) < 0
		|			ТОГДА 0
		|		ИНАЧЕ ЕСТЬNULL(ЗапасыИПотребностиОстатки.ВНаличииОстаток, 0) -
		|			ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток, 0) -
		|			ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьПоМереПоступленияОстаток, 0)
		|	КОНЕЦ КАК Остаток,
		|	ВЫБОР
		|		КОГДА ДанныеТЧ.ВидЦены.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
		|			ТОГДА ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВПути,
		|	ВЫБОР
		|		КОГДА ДанныеТЧ.ВидЦены.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА ЕСТЬNULL(ЗапасыИПотребностиОстатки.ЗаказаноОстаток, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Распр,
		|	ДанныеТЧ.Цена КАК Цена,
		|	ДанныеТЧ.Сумма КАК Сумма,
		|	ДанныеТЧ.ДатаОтгрузки КАК ДатаОтгрузки,
		|	НоменклатураСпр.Артикул КАК Артикул,
		|	НоменклатураСпр.Производитель КАК Производитель,
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КСборкеОстаток, 0) + ЕСТЬNULL(ТоварыКОтгрузкеОстатки.СобираетсяОстаток, 0) КАК
		|		Собирается,
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстатки.СобраноОстаток, 0) КАК Собрано,
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КОформлениюОстаток, 0) КАК Оформить,
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток, 0) КАК Отгрузить
		|ИЗ
		|	ДанныеТЧ КАК ДанныеТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(, (Номенклатура, Характеристика, Склад) В
		|			(ВЫБРАТЬ
		|				ДанныеТЧ.Номенклатура КАК Номенклатура,
		|				ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|				ДанныеТЧ.Склад КАК Склад
		|			ИЗ
		|				ДанныеТЧ КАК ДанныеТЧ)) КАК ЗапасыИПотребностиОстатки
		|		ПО ДанныеТЧ.Номенклатура = ЗапасыИПотребностиОстатки.Номенклатура
		|		И ДанныеТЧ.Назначение = ЗапасыИПотребностиОстатки.Назначение
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
		|		ПО ДанныеТЧ.Номенклатура = НоменклатураСпр.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности КАК ЗапасыИПотребности
		|		ПО ДанныеТЧ.Ссылка = ЗапасыИПотребности.Заказ
		|		И ДанныеТЧ.Номенклатура = ЗапасыИПотребности.Номенклатура
		|		И ДанныеТЧ.Назначение = ЗапасыИПотребности.Назначение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке.Остатки(, (ДокументОтгрузки, Номенклатура) В
		|			(ВЫБРАТЬ
		|				ДанныеТЧ.Ссылка КАК ДокументОтгрузки,
		|				ДанныеТЧ.Номенклатура КАК Номенклатура
		|			ИЗ
		|				ДанныеТЧ КАК ДанныеТЧ)) КАК ТоварыКОтгрузкеОстатки
		|		ПО ДанныеТЧ.Ссылка = ТоварыКОтгрузкеОстатки.ДокументОтгрузки
		|		И ДанныеТЧ.Номенклатура = ТоварыКОтгрузкеОстатки.Номенклатура
		|ГДЕ
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КОформлениюОстаток, 0) <> 0
		|	Или ЕСТЬNULL(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток, 0) <> 0
		|	ИЛИ ЕСТЬNULL(ЗапасыИПотребностиОстатки.КОбеспечениюОстаток, 0) <> 0
		|	ИЛИ ЕСТЬNULL(ЗапасыИПотребностиОстатки.РезервироватьНаСкладеОстаток, 0) <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьГрузоотправителя()

Функция ПолучитьМассивСтруктурТыблицыДвиженийДляСпискаЗаказовКлиента(Ссылка) Экспорт
	
	ТаблицаДвижений = ПолучитьТаблицуДвиженийДляСпискаЗаказовКлиента(Ссылка);
	
	Возврат ЕТС_ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаДвижений);
		
КонецФункции