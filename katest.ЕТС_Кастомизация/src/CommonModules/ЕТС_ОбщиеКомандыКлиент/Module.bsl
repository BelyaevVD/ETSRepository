
Процедура ТаможеннаяДекларацияИмпорт_ЗагрузитьИзФайла(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОкончаниеВыбораСписка", ЕТС_ОбщиеКомандыКлиент, Форма);
	
	ОткрытьФорму("ОбщаяФорма.ЕТС_ФормаВыбораПроизводителя", , , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ТаможеннаяДекларацияИмпорт_ЗагрузитьИзФайлаБезПроизводителя(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Объект = Форма.Объект;
	
	Если Не ЗначениеЗаполнено(Объект.НомерДекларации) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан номер декларации!");
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	// Зададим фильтр для диалога.
	ТекстФильтра = "(*.xml)|*.xml";
				   
	Диалог.Фильтр = ТекстФильтра;
	
	Если Диалог.Выбрать() Тогда
			// Сохраняет имя выбранного файла
		ИмяФайла = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		Сообщить("Выберите файл!",СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	ФайлБинДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	Если Не (Объект.Товары.Количество() >= 1 И ЗначениеЗаполнено(Объект.Товары[0].ДокументПоступления))  Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документ нужно ввести на основании!");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("ПТУ"							,Объект.Товары[0].ДокументПоступления);
	ПараметрыФормирования.Вставить("ИспользоватьДопАртикул"			,Форма.ИспользоватьДопАртикул);
	ПараметрыФормирования.Вставить("УникальныйИдентификатор"		,Форма.УникальныйИдентификатор);
	ПараметрыФормирования.Вставить("ФайлXML"						,ФайлБинДанные);
	ПараметрыФормирования.Вставить("НомерДекларации"				,Объект.НомерДекларации);
	
	ДлительнаяОперация = ЕТС_ОбщиеКомандыСервер.ПрочитатьНаСервереТаможеннуюДекларацию(ПараметрыФормирования);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, 
		Новый ОписаниеОповещения("ПослеЧтенияТаможеннойДекларации", ЕТС_ОбщиеКомандыКлиент, Форма), 
		ПараметрыОжидания);
	
КонецПроцедуры

Процедура ОкончаниеВыбораСписка(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Объект = ДополнительныеПараметры.Объект;
	
	Если Не ЗначениеЗаполнено(Объект.НомерДекларации) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан номер декларации!");
		Возврат;
	КонецЕсли;
	
	Если РезультатЗакрытия <> Неопределено Тогда
		СписокПроизводителей = РезультатЗакрытия;
	Иначе	
		СписокПроизводителей = Новый СписокЗначений;
	КонецЕсли;
	
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	// Зададим фильтр для диалога.
	ТекстФильтра = "(*.xml)|*.xml";
				   
	Диалог.Фильтр = ТекстФильтра;
	
	Если Диалог.Выбрать() Тогда
			// Сохраняет имя выбранного файла
		ИмяФайла = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		Сообщить("Выберите файл!",СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	ФайлБинДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	Если Не (Объект.Товары.Количество() >= 1 И ЗначениеЗаполнено(Объект.Товары[0].ДокументПоступления))  Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документ нужно ввести на основании!");
		Возврат;
	КонецЕсли;
	
	Если СписокПроизводителей.Количество() < 1 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбраны производители!");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("ПТУ"							,Объект.Товары[0].ДокументПоступления);
	ПараметрыФормирования.Вставить("СписокПроизводителей"			,СписокПроизводителей);
	ПараметрыФормирования.Вставить("ИспользоватьДопАртикул"			,ДополнительныеПараметры.ИспользоватьДопАртикул);
	ПараметрыФормирования.Вставить("УникальныйИдентификатор"		,ДополнительныеПараметры.УникальныйИдентификатор);
	ПараметрыФормирования.Вставить("ФайлXML"						,ФайлБинДанные);
	ПараметрыФормирования.Вставить("НомерДекларации"				,Объект.НомерДекларации);
	
	ДлительнаяОперация = ЕТС_ОбщиеКомандыСервер.ПрочитатьНаСервереТаможеннуюДекларацию(ПараметрыФормирования);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, 
		Новый ОписаниеОповещения("ПослеЧтенияТаможеннойДекларации", ЕТС_ОбщиеКомандыКлиент, ДополнительныеПараметры), 
		ПараметрыОжидания);
	
КонецПроцедуры
	
Процедура ПослеЧтенияТаможеннойДекларации(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = Неопределено Тогда // Пользователь отменил задание.
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ДанныеТЧ = ЕТС_ОбщиеКомандыСервер.ПослеЧтенияНаСервере(Результат.АдресРезультата);
	
	Если ДанныеТЧ.Разделы <> Неопределено И ДанныеТЧ.Разделы.Количество() > 0 Тогда
		ДополнительныеПараметры.Объект.Разделы.Очистить();
		Для каждого Стр Из ДанныеТЧ.Разделы Цикл
			ЗаполнитьЗначенияСвойств(ДополнительныеПараметры.Объект.Разделы.Добавить(),Стр);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеТЧ.Товары <> Неопределено И ДанныеТЧ.Товары.Количество() > 0 Тогда
		ДополнительныеПараметры.Объект.Товары.Очистить();
		Для каждого Стр Из ДанныеТЧ.Товары Цикл
			ЗаполнитьЗначенияСвойств(ДополнительныеПараметры.Объект.Товары.Добавить(),Стр);
		КонецЦикла;
	КонецЕсли;

	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	
КонецПроцедуры
	
Процедура ЗаказКлиента_ЕТС_СвязанныеДокументыКоманда(Форма, Команда, ДополнительныеПараметры) Экспорт

	КомандаСвязанныеДокументы = Форма.Команды.Найти("ПодменюОтчетыСмТакже__Авто_DFF4A1BC964F18C76E1B5EA04556DBE9");
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(Форма, КомандаСвязанныеДокументы, Форма.Объект);

КонецПроцедуры

Процедура ЗаказКлиентаСписок_ЕТС_СвязанныеДокументыКоманда(Форма, Команда, ДополнительныеПараметры) Экспорт

	КомандаСвязанныеДокументы = Форма.Команды.Найти("ПодменюОтчетыСмТакже__Авто_DFF4A1BC964F18C76E1B5EA04556DBE9");
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(Форма, КомандаСвязанныеДокументы, Форма.Элементы.Список);

КонецПроцедуры

Процедура ЗаказКлиентаСписок_ЕТС_Обновить(Форма, Команда, ДополнительныеПараметры) Экспорт

	ТекСтрока = Форма.Элементы.Список.ТекущиеДанные;
	Форма.ТаблицаДвижений.Очистить();
	МассивСтруктур = ЕТС_ОбщиеКомандыСервер.ПолучитьМассивСтруктурТыблицыДвиженийДляСпискаЗаказовКлиента(ТекСтрока.Ссылка);
	Для Каждого Стр Из МассивСтруктур Цикл
		НоваяСтрока = Форма.ТаблицаДвижений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);	
	КонецЦикла;
	Форма.Элементы.Список.Обновить();
КонецПроцедуры

Процедура ЗаказКлиента_ЕТС_ЗаполнитьПоОстаткам(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Если Форма.Модифицированность = Истина Или Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документ не записан, для заполнения необходимо записать документ!");
	Иначе 	
		ТЧТовары = ЕТС_ОбщиеКомандыСервер.ЗаполнитьЗаказКлиента(Форма.Объект.Ссылка);
		Форма.Объект.Товары.Очистить();
		Для каждого Стр Из ТЧТовары Цикл
			ЗаполнитьЗначенияСвойств(Форма.Объект.Товары.Добавить(),Стр);
		КонецЦикла;
		Форма.Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры
	
Процедура ЗаказКлиента_ЕТС_ТоварыВидЦены_НачалоВыбора(Форма, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОкончаниеВыбораСпискаПрайса", ЕТС_ОбщиеКомандыКлиент, Форма);
	
	ОткрытьФорму("ОбщаяФорма.ЕТС_ФормаВыбораВидаЦены", ДополнительныеПараметры , , , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОкончаниеВыбораСпискаПрайса(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	НомерСтроки = ДополнительныеПараметры.Элементы.Товары.ТекущиеДанные.НомерСтроки;
	СтрокаТЧ = ДополнительныеПараметры.Объект.Товары[НомерСтроки-1];
	Если РезультатЗакрытия <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТЧ,РезультатЗакрытия);
		СтрокаТЧ.ДатаОтгрузки = ДополнительныеПараметры.Объект.Дата + РезультатЗакрытия.ДнейДоставки * 86400;
		Форма = ДополнительныеПараметры;
		Форма.Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаказКлиента_ЕТС_СформироватьЗаказыПоПотребностям(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Если Форма.Модифицированность = Истина Или Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Или Не Форма.Объект.Проведен Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документ не проведен, для формирования необходимо провести документ!");
	Иначе 	
		ЕТС_ОбщиеКомандыСервер.СоздатьДокументыПотребностейНаОснованииЗаказаКлиента(Форма.Объект.Ссылка);
		ПоказатьПредупреждение(,"Документы потребностей созданы!",10,"Внимание!");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЕТС_СоздатьДопСвойстваДоговора(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Для создания доп свойств необходимо сначала записать элемент справочника!");
	Иначе 	
		СпрДопСвойства = ЕТС_ОбщиеКомандыСервер.ЕТС_СоздатьЭлементСправочникаДопСвойстваДоговора(Форма.Объект.Ссылка);
		Форма["ЕТС_ДопСвойстваДоговора"] = СпрДопСвойства;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаказПоставщика_ЕТС_ЗаполнитьИзЗаказов(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен контрагент!");
	Иначе 	
		ТЧТовары = ЕТС_ОбщиеКомандыСервер.ЗаполнитьЗаказПоставщикуИзЗаказовКлиентам(Форма.Объект.Партнер);
		Форма.Объект.Товары.Очистить();
		Для каждого Стр Из ТЧТовары Цикл
			ЗаполнитьЗначенияСвойств(Форма.Объект.Товары.Добавить(),Стр);
		КонецЦикла;
		Форма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеремещениеТоваров_ЕТС_ЗаполнитьРезервами(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Если Форма.Модифицированность = Истина Или Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Документ не записан, для заполнения необходимо записать документ!");
	Иначе
		ПараметрыОтбора = Новый Структура("СкладОтправитель,СкладПолучатель",Форма.Объект.СкладОтправитель,Форма.Объект.СкладПолучатель);
		
		ТЧТовары = ЕТС_ОбщиеКомандыСервер.ПеремещениеТоваров_ЕТС_ЗаполнитьРезервами(ПараметрыОтбора);
		Форма.Объект.Товары.Очистить();
		Для каждого Стр Из ТЧТовары Цикл
			ЗаполнитьЗначенияСвойств(Форма.Объект.Товары.Добавить(),Стр);
		КонецЦикла;
		Форма.Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаказКлиента_ЕТС_ЗагрузитьССайта(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	ЕТС_ОбщиеКомандыСервер.ЗагрузитьССайтаЗаказКлиента();
	Форма.Элементы.Список.Обновить();

КонецПроцедуры

Процедура ЭлектронныйДокументВходящийЭДО_ЕТС_ПровестиДокументыУчета(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	МассивДокументов = Новый Массив;
	Для Каждого стр Из Форма.ДокументыОснования Цикл
		МассивДокументов.Добавить(стр.ОбъектУчета);
	КонецЦикла;
	ЕТС_ОбщиеКомандыСервер.ПровестиДокументыЭДО(МассивДокументов);
	//Форма.Элементы.Список.Обновить();

КонецПроцедуры
