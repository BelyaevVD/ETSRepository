Процедура ВыгрузитьОбъект(МассивСтруктур) Экспорт 

	Данные = Новый Структура;
	Данные.Вставить("items",МассивСтруктур);
	
	json = createJson(Данные);
	httpPost(json,МассивСтруктур);
	
КонецПроцедуры

Функция createJson(Данные) 
	
	Запись = Новый ЗаписьJSON;
    тПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет, " ", Истина);  
    Запись.УстановитьСтроку(тПараметрыJSON); 
	json =  Новый Структура;
	json.Вставить("params",Данные);
	ЗаписатьJSON(Запись, json);
    СтрокаJS = Запись.Закрыть();
	
	Возврат СтрокаJS;

КонецФункции	

Процедура httpPost(json,МассивСтруктур) 
	
	Обработан = Ложь;
	Ошибка = "";
	
	Соединение = Новый HTTPСоединение("10.0.20.150",,,,,5);
	Запрос = Новый HTTPЗапрос("api/event/ka");
    Запрос.Заголовки.Вставить("Content-type", "application/json");
    Запрос.УстановитьТелоИзСтроки(json,КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
	
    Попытка
		ОтветСервера = Соединение.ВызватьHTTPМетод("POST", Запрос);
		
		
		Если ОтветСервера.КодСостояния = 200 Тогда
			ТелоОтвета = ОтветСервера.ПолучитьТелоКакСтроку();
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
			ИспрДанные = ПрочитатьJSON(ЧтениеJSON,,,,"ФункцияВосстановленияJSON", ЕТС_ВыгрузкаНаСайт);
            Если ИспрДанные.result Тогда
				Обработан = Истина;
			КонецЕсли;
		Иначе
			Ошибка = "Код состояния " + ОтветСервера.КодСостояния + ".";
		КонецЕсли;
		
	Исключение
    	Ошибка = ОписаниеОшибки();
	КонецПопытки;
	
	Для каждого Стр Из МассивСтруктур Цикл
		НаборЗаписей = РегистрыСведений.ЕТС_ОбменССайтом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИмяМетаданных.Установить(Стр.documentName);
		НаборЗаписей.Отбор.GUID.Установить(Стр.GUID);
		НаборЗаписей.Прочитать();
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			СтрокаНабора.Ошибка = Ошибка;
			СтрокаНабора.Обработан = Обработан;
			СтрокаНабора.ДатаОбработки = ТекущаяДата();
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция ФункцияВосстановленияJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если СтрНайти(Свойство,"date") > 0  Тогда // В название свойства есть Дата
		Возврат ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	ИначеЕсли Свойство = "base" Тогда //пример с валютой
		Возврат Справочники.Валюты.НайтиПоНаименованию(Значение);	
	КонецЕсли;      
	
КонецФункции
