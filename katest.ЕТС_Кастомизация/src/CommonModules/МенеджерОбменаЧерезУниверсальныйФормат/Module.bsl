
&После("ПКО_Справочник_ДоговорыКонтрагентов_ПриОтправкеДанных")
Процедура ЕТС_ПКО_Справочник_ДоговорыКонтрагентов_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	Если ДанныеИБ.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
		Если УправлениеСвойствами.ЗначениеСвойства(ДанныеИБ.Ссылка, "СКомиссионеромНаЗакупку") = Истина Тогда
			ДанныеXDTO.КлючевыеСвойства.Вставить("ВидДоговора", "СКомиссионеромНаЗакупку");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&Вместо("ПКО_Справочник_СтатьиРасходов_ПриОтправкеДанных")
Процедура ЕТС_ПКО_Справочник_СтатьиРасходов_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)
	ДанныеБП = УправлениеСвойствами.ЗначениеСвойства(ДанныеИБ.Ссылка, "СтатьяРасходаБП");
	Если ЗначениеЗаполнено(ДанныеБП) Тогда
		ДанныеИБ = ДанныеБП;
		ДанныеXDTO.Вставить("Ссылка",       ДанныеИБ);
		ДанныеXDTO.Вставить("Наименование", ДанныеИБ.Наименование);
		ДанныеXDTO.Вставить("КодВПрограмме",ДанныеИБ.Код);
	КонецЕсли;
	ПродолжитьВызов(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки);
КонецПроцедуры


&ИзменениеИКонтроль("ПОД_Документ_ПроизводствоБезЗаказа_Отправка_ПриОбработке")
Процедура ЕТС_ПОД_Документ_ПроизводствоБезЗаказа_Отправка_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)
	УстановитьИспользованиеПКО(ИспользованиеПКО, Ложь);
	ИспользоватьПлановуюСтоимость = Ложь;

	ПараметрыПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату("УчетнаяПолитикаФинансовогоУчета", 
	ДанныеИБ.Организация, 
	ДанныеИБ.Дата);
	Если ПараметрыПолитики <> Неопределено Тогда
		ИспользоватьПлановуюСтоимость = ПараметрыПолитики.УчетГотовойПродукцииПоПлановойСтоимости;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеИБ.Ссылка);
	Запрос.УстановитьПараметр("Дата", ДанныеИБ.Дата);
	Запрос.УстановитьПараметр("ИспользоватьПлановуюСтоимость", ИспользоватьПлановуюСтоимость);
	Запрос.УстановитьПараметр("ВидЦеныПлановойСебестоимости", ПроизводствоСерверПовтИсп.ВидЦеныПлановойСтоимости());
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеИБ.Организация));

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	Склады.Ссылка КАК Склад,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.Количество КАК Количество,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.ДоляСтоимости КАК ДоляСтоимости,
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.Ссылка.Валюта КАК ВалютаДокумента,
	|	ЕСТЬNULL(ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.Ссылка.ВидЦены.ВалютаЦены, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаЦены,
	|	ВЫБОР
	|		КОГДА ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
	|		ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
	|	КОНЕЦ КАК ТипЗапасов
#Вставка
	|,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Цена КАК Цена,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Сумма КАК Сумма,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Упаковка КАК Упаковка
#КонецВставки
	|ПОМЕСТИТЬ втПродукция
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВидыЗапасовВыходныеИзделия КАК ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.Получатель = Склады.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ПроизводствоБезЗаказаВыходныеИзделия
	|		ПО (ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура = ПроизводствоБезЗаказаВыходныеИзделия.Номенклатура
	|				И ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Характеристика = ПроизводствоБезЗаказаВыходныеИзделия.Характеристика
	|				И ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.АналитикаУчетаНоменклатуры.Серия = ПроизводствоБезЗаказаВыходныеИзделия.Серия)
	|ГДЕ
	|	ПроизводствоБезЗаказаВидыЗапасовВыходныеИзделия.Ссылка = &Ссылка
	|	И НЕ ЕСТЬNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|	И ПроизводствоБезЗаказаВыходныеИзделия.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПроизводствоБезЗаказаВыходныеИзделия.НомерСтроки,
	|	ПроизводствоБезЗаказаВыходныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ПроизводствоБезЗаказаВыходныеИзделия.АналитикаУчетаНоменклатуры.Характеристика,
	|	ПроизводствоБезЗаказаВыходныеИзделия.АналитикаУчетаНоменклатуры.Серия,
	|	ПроизводствоБезЗаказаВыходныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения,
	|	ПроизводствоБезЗаказаВыходныеИзделия.НаправлениеВыпуска,
	|	Склады.Ссылка,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Количество,
	|	ПроизводствоБезЗаказаВыходныеИзделия.ДоляСтоимости,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Ссылка.Валюта,
	|	ЕСТЬNULL(ПроизводствоБезЗаказаВыходныеИзделия.Ссылка.ВидЦены.ВалютаЦены, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)),
	|	ВЫРАЗИТЬ(""ГотоваяПродукция"" КАК СТРОКА(20))
#Вставка
	|,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Цена КАК Цена,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Сумма КАК Сумма,
	|	ПроизводствоБезЗаказаВыходныеИзделия.Упаковка КАК Упаковка
#КонецВставки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ПроизводствоБезЗаказаВыходныеИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ПроизводствоБезЗаказаВыходныеИзделия.Получатель = Склады.Ссылка
	|ГДЕ
	|	ПроизводствоБезЗаказаВыходныеИзделия.Ссылка = &Ссылка
	|	И НЕ ЕСТЬNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|	И ПроизводствоБезЗаказаВыходныеИзделия.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтрокиДокумента,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВложенныйЗапрос.ТипЗапасов КАК ТипЗапасов,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(31, 2)) КАК Цена,
	|	(ВЫРАЗИТЬ(ВложенныйЗапрос.Цена / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(31, 2))) * ВложенныйЗапрос.Количество КАК Сумма
	|ПОМЕСТИТЬ втПродукцияСоСтоимостью
	|ИЗ
	|	(ВЫБРАТЬ
	|		втТовары.НомерСтроки КАК НомерСтроки,
	|		втТовары.Номенклатура КАК Номенклатура,
	|		втТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		втТовары.Характеристика КАК Характеристика,
	|		втТовары.Серия КАК Серия,
	|		втТовары.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|		втТовары.Склад КАК Склад,
	|		втТовары.Количество КАК Количество,
	|		втТовары.ТипЗапасов КАК ТипЗапасов,
#Удаление
	|		ЦеныНоменклатурыСрезПоследних.Упаковка КАК УпаковкаЦены,
	|		ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) * ВЫБОР
	|			КОГДА втТовары.ВалютаДокумента = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|					ИЛИ втТовары.ВалютаЦены = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ТОГДА 0
	|			КОГДА втТовары.ВалютаДокумента = втТовары.ВалютаЦены
	|				ТОГДА 1
	|			ИНАЧЕ КурсыВалютыЦены.КурсЧислитель * КурсыВалютыДокумента.КурсЗнаменатель / (КурсыВалютыДокумента.КурсЧислитель * КурсыВалютыЦены.КурсЗнаменатель)
	|		КОНЕЦ КАК Цена
#КонецУдаления
#Вставка
	|	втТовары.Упаковка КАК УпаковкаЦены,
	|	втТовары.Цена КАК Цена
#КонецВставки
	|	ИЗ
	|		втПродукция КАК втТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ВидЦены = &ВидЦеныПлановойСебестоимости) КАК ЦеныНоменклатурыСрезПоследних
	|			ПО втТовары.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|				И втТовары.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|				И (&ИспользоватьПлановуюСтоимость)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютыДокумента
	|			ПО (&ИспользоватьПлановуюСтоимость)
	|				И втТовары.ВалютаДокумента = КурсыВалютыДокумента.Валюта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютыЦены
	|			ПО (&ИспользоватьПлановуюСтоимость)
	|				И втТовары.ВалютаЦены = КурсыВалютыЦены.Валюта
	|	ГДЕ
	|		НЕ втТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтрокиДокумента,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Количество > 0
	|			ТОГДА ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма / ВложенныйЗапрос.Количество КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.ТипЗапасов КАК ТипЗапасов
	|ПОМЕСТИТЬ втВозвратныеОтходы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.НомерСтроки КАК НомерСтроки,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.Количество КАК Количество,
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.Сумма КАК Сумма,
	|		Склады.Ссылка КАК Склад,
	|		ВЫБОР
	|			КОГДА ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
	|			ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
	|		КОНЕЦ КАК ТипЗапасов
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВидыЗапасовПобочныеИзделия КАК ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.Получатель = Склады.Ссылка
	|	ГДЕ
	|		ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.Ссылка = &Ссылка
	|		И НЕ ЕСТЬNULL(Склады.ЦеховаяКладовая, ЛОЖЬ)
	|		И НЕ ПроизводствоБезЗаказаВидыЗапасовПобочныеИзделия.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.ТипЗапасов КАК ТипЗапасов,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	ТаблицаТовары.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма
	|ИЗ
	|	втПродукцияСоСтоимостью КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.ТипЗапасов КАК ТипЗапасов,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	ТаблицаТовары.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма
	|ИЗ
	|	втВозвратныеОтходы КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ТипЗапасов КАК ТипЗапасов,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	&ТекстЗапросаРНПТ,
	|	&ТекстЗапросаГТД,
	|	ТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.ПодразделениеЗатрат КАК ПодразделениеЗатрат
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.НомерСтроки КАК НомерСтроки,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.НомерГТД КАК НомерГТД,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ВЫБОР
	|			КОГДА ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА ВЫРАЗИТЬ(""КомиссионныеТовары"" КАК СТРОКА(20))
	|			КОГДА ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|				ТОГДА ""ДавальческиеСырьеИМатериалы""
	|			ИНАЧЕ ВЫРАЗИТЬ(&СобственныеТовары КАК СТРОКА(20))
	|		КОНЕЦ КАК ТипЗапасов,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.Количество КАК Количество,
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.Подразделение КАК ПодразделениеЗатрат
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВидыЗапасовМатериалыИРаботы КАК ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы
	|	ГДЕ
	|		ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.Ссылка = &Ссылка
	|		И НЕ ЕСТЬNULL(ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.МестоХранения.ЦеховаяКладовая, ЛОЖЬ)
	|		И НЕ ПроизводствоБезЗаказаВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))) КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	&ТекстЗапросаХарактеристика,
	|	&ТекстЗапросаСерия,
	|	ТаблицаТовары.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.ТипЗапасов КАК ТипЗапасов
	|ИЗ
	|	(ВЫБРАТЬ
	|		втПродукцияСоСтоимостью.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|		втПродукцияСоСтоимостью.Номенклатура КАК Номенклатура,
	|		втПродукцияСоСтоимостью.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		втПродукцияСоСтоимостью.Характеристика КАК Характеристика,
	|		втПродукцияСоСтоимостью.Серия КАК Серия,
	|		втПродукцияСоСтоимостью.НаправлениеВыпуска КАК НаправлениеВыпуска,
	|		втПродукцияСоСтоимостью.Склад КАК Склад,
	|		втПродукцияСоСтоимостью.Количество КАК Количество,
	|		втПродукцияСоСтоимостью.Цена КАК Цена,
	|		втПродукцияСоСтоимостью.Сумма КАК Сумма,
	|		втПродукцияСоСтоимостью.ТипЗапасов КАК ТипЗапасов,
	|		1 КАК Приоритет
	|	ИЗ
	|		втПродукцияСоСтоимостью КАК втПродукцияСоСтоимостью
	|	ГДЕ
	|		втПродукцияСоСтоимостью.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втВозвратныеОтходы.НомерСтрокиДокумента,
	|		втВозвратныеОтходы.Номенклатура,
	|		втВозвратныеОтходы.ЕдиницаИзмерения,
	|		втВозвратныеОтходы.Характеристика,
	|		втВозвратныеОтходы.Серия,
	|		втВозвратныеОтходы.НаправлениеВыпуска,
	|		втВозвратныеОтходы.Склад,
	|		втВозвратныеОтходы.Количество,
	|		втВозвратныеОтходы.Цена,
	|		втВозвратныеОтходы.Сумма,
	|		втВозвратныеОтходы.ТипЗапасов,
	|		2
	|	ИЗ
	|		втВозвратныеОтходы КАК втВозвратныеОтходы
	|	ГДЕ
	|		втВозвратныеОтходы.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)) КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Количество КАК Количество,
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Подразделение КАК ПодразделениеЗатрат
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ПроизводствоБезЗаказаМатериалыИРаботы
	|ГДЕ
	|	ПроизводствоБезЗаказаМатериалыИРаботы.Ссылка = &Ссылка
	|	И ПроизводствоБезЗаказаМатериалыИРаботы.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))";

	ЗаполнитьСобственныеТоварыВТексте(ТекстЗапроса);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	"&ТекстЗапросаКоэффициентУпаковки",
	Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
	"ВложенныйЗапрос.УпаковкаЦены",
	"ВложенныйЗапрос.Номенклатура"));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаХарактеристика",
	ПолучитьТекстЗапросаХарактеристики(КомпонентыОбмена, "ТаблицаТовары"));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСерия",
	ПолучитьТекстЗапросаСерии(КомпонентыОбмена, "ТаблицаТовары"));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРНПТ",
	ПолучитьТекстЗапросаРНПТ(КомпонентыОбмена, "ТаблицаТовары"));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаГТД",
	ПолучитьТекстЗапросаГТД(КомпонентыОбмена, "ТаблицаТовары"));

	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.ВыполнитьПакет();

	ТаблицаПродукция          = РезультатЗапроса[РезультатЗапроса.Количество() - 5].Выгрузить();
	ТаблицаВозвратныеОтходы   = РезультатЗапроса[РезультатЗапроса.Количество() - 4].Выгрузить();
	ТаблицаМатериалы          = РезультатЗапроса[РезультатЗапроса.Количество() - 3].Выгрузить();
	ТаблицаПродукцииКСписанию = РезультатЗапроса[РезультатЗапроса.Количество() - 2].Выгрузить();
	ТаблицаРаботы             = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выгрузить();

	СкладДляВыгрузки = Неопределено;
	НаправлениеВыпуска = Неопределено;
	Если ТаблицаПродукция.Количество() > 0 Тогда
		ИспользованиеПКО.Документ_ПроизводствоБезЗаказа_Отправка = Истина;
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаПродукция, Истина, Ложь);

		// Склад и направление выпуска будем брать из первой строки выборки.
		СкладДляВыгрузки = СкладДляВыгрузки(ТаблицаПродукция[0].Склад, КомпонентыОбмена.ПараметрыКонвертации);
		НаправлениеВыпуска = ТаблицаПродукция[0].НаправлениеВыпуска;

	КонецЕсли;

	Если ТаблицаВозвратныеОтходы.Количество() > 0 Тогда
		ИспользованиеПКО.Документ_ПроизводствоБезЗаказа_Отправка = Истина;
		Если НЕ ЗначениеЗаполнено(СкладДляВыгрузки) Тогда
			СкладДляВыгрузки = СкладДляВыгрузки(ТаблицаВозвратныеОтходы[0].Склад, КомпонентыОбмена.ПараметрыКонвертации);	
		КонецЕсли;

		Если НЕ ЗначениеЗаполнено(НаправлениеВыпуска) Тогда
			НаправлениеВыпуска = ТаблицаВозвратныеОтходы[0].НаправлениеВыпуска;	
		КонецЕсли;

		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаВозвратныеОтходы, Истина, Ложь);
	КонецЕсли;

	Если ТаблицаМатериалы.Количество() > 0 Тогда
		ИспользованиеПКО.Документ_ПроизводствоБезЗаказа_Отправка = Истина;
		Если НЕ ЗначениеЗаполнено(НаправлениеВыпуска) Тогда
			НаправлениеВыпуска = Перечисления.ХозяйственныеОперации.ВыпускПродукцииНаСклад;	
		КонецЕсли;

		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаМатериалы, Истина, Ложь);
	КонецЕсли;

	Если ТаблицаПродукцииКСписанию.Количество() > 0 Тогда
		ЗаполнитьХарактеристикуУпаковкуВТЧ(КомпонентыОбмена, ТаблицаПродукцииКСписанию, Истина, Ложь);
		ИспользованиеПКО.Документ_ПроизводствоБезЗаказа_СписаниеЗапасов = Истина;
	Конецесли;

	Если ТаблицаРаботы.Количество() > 0 Тогда
		ИспользованиеПКО.Документ_ПроизводствоБезЗаказа_Отправка = Истина;
	КонецЕсли;

	ДанныеИБ.ДополнительныеСвойства.Вставить("ТаблицаПродукция", ТаблицаПродукция);
	ДанныеИБ.ДополнительныеСвойства.Вставить("ТаблицаВозвратныеОтходы", ТаблицаВозвратныеОтходы);
	ДанныеИБ.ДополнительныеСвойства.Вставить("ТаблицаМатериалы", ТаблицаМатериалы);
	ДанныеИБ.ДополнительныеСвойства.Вставить("ТаблицаПродукцииКСписанию", ТаблицаПродукцииКСписанию);
	ДанныеИБ.ДополнительныеСвойства.Вставить("ТаблицаРаботы", ТаблицаРаботы);

	ДанныеИБ.ДополнительныеСвойства.Вставить("СкладДляВыгрузки", СкладДляВыгрузки);
	ДанныеИБ.ДополнительныеСвойства.Вставить("НаправлениеВыпуска", ?(НаправлениеВыпуска = Неопределено, Перечисления.ХозяйственныеОперации.ВыпускПродукцииНаСклад, НаправлениеВыпуска));
КонецПроцедуры

