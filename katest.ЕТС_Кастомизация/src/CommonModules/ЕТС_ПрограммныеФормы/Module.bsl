
Процедура ДокументДополнитьФорму(Форма) Экспорт

	Элементы = Форма.Элементы;
	
	МассивДобавляемыхРеквизитов = Новый Массив;
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("СправочникСсылка.Склады"));
	СписокТипов.Добавить(Тип("СправочникСсылка.Партнеры"));
	ОписаниеСоставногоТипа = Новый ОписаниеТипов(СписокТипов);
	
	//МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("Поставщик", ОписаниеСоставногоТипа,"Объект.Товары" ,"Поставщик"));
	МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("Поставщик", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(20)),"Объект.Товары" ,"Поставщик"));
	МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("Остаток", Новый ОписаниеТипов("Число", ,,Новый КвалификаторыЧисла(15,2)), "Объект.Товары" ,"Остаток"));
	//МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("Отказ", Новый ОписаниеТипов("Булево"), "Объект.Товары" ,"Отказ"));
	
	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	
	Если Форма.ИмяФормы = "Документ.КоммерческоеПредложениеКлиенту.Форма.ФормаДокумента" Тогда
		НоваяКолонка = Элементы.Вставить("ТоварыОтказ", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыВидЦены);
		НоваяКолонка.ПутьКДанным = "Объект.Товары.ЕТС_Отказ";
		НоваяКолонка.Вид = ВидПоляФормы.ПолеФлажка;
		НоваяКолонка.Заголовок = "Отказ";
		
		НоваяКолонка = Элементы.Добавить("ТоварыАвиаПеревозка", Тип("ПолеФормы"), Элементы.Товары);
		НоваяКолонка.ПутьКДанным = "Объект.Товары.ЕТС_АвиаПеревозка";
		НоваяКолонка.Вид = ВидПоляФормы.ПолеФлажка;
		НоваяКолонка.Заголовок = "Авиа перевозка";
		
		НоваяКолонка = Элементы.Добавить("ТоварыЗакупочнаяЦена", Тип("ПолеФормы"), Элементы.Товары);
		НоваяКолонка.ПутьКДанным = "Объект.Товары.ЕТС_ЗакупочнаяЦена";
		НоваяКолонка.Вид = ВидПоляФормы.ПолеВвода;
		НоваяКолонка.Заголовок = "Закупочная цена";
	КонецЕсли;
	
	НоваяКолонка = Элементы.Вставить("ТоварыОстаток", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыВидЦены);
	НоваяКолонка.ПутьКДанным = "Объект.Товары.Остаток";
	НоваяКолонка.Заголовок = "Остаток";
	
	НоваяКолонка = Элементы.Вставить("ТоварыПоставщик", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыВидЦены);
	НоваяКолонка.ПутьКДанным = "Объект.Товары.Поставщик";
	НоваяКолонка.Заголовок = "Поставщик";
	
КонецПроцедуры

Функция  ДокументДополнитьТЧТоварыРеквизитами(СсылкаНаДокумент) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументТовары.*
		|ПОМЕСТИТЬ ДанныеТЧ
		|ИЗ
		|	Документ.#.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляПартнера)
		|			ТОГДА ВидыЦен.ЕТС_Партнер.Наименование
		|		КОГДА ВидыЦен.Назначение = ЗНАЧЕНИЕ(Перечисление.НазначенияВидовЦен.ИндивидуальныйДляСклада)
		|			ТОГДА ВидыЦен.ЕТС_Склад.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Поставщик,
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Остаток,
		|	ДанныеТЧ.*
		|ИЗ
		|	ДанныеТЧ КАК ДанныеТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				&ДатаОстатков,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ДанныеТЧ.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ДанныеТЧ КАК ДанныеТЧ)) КАК ТоварыНаСкладахОстатки
		|		ПО ДанныеТЧ.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО ДанныеТЧ.ВидЦены = ВидыЦен.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеТЧ.НомерСтроки";
	
	Запрос.УстановитьПараметр("ДатаОстатков", ТекущаяДата());
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	ОбъектОписания = Метаданные.НайтиПоТипу(ТипЗнч(СсылкаНаДокумент));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#", ОбъектОписания.Имя);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПолучитьОстатокПоНоменклатуре(Номенклатура) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Номенклатура = &Номенклатура) КАК ТоварыНаСкладахОстатки";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе 
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Остаток;
	КонецЕсли;

КонецФункции // ПолучитьОстатокПоНоменклатуре()

Функция ПолучитьМакетНаСервере(ИмяМакета) Экспорт
	Макет = ПолучитьОбщийМакет(ИмяМакета);
	Возврат Макет;
КонецФункции

Процедура ОграничитьФормуСпискаПоСкладам(Список, ИмяМетаданных) Экспорт
	
	Список.ТекстЗапроса = Список.ТекстЗапроса + 
	
		//"
		//|ГДЕ
		//|	&ИмяМетаданных.Склад В (&СписокСкладов) Или &ЕТС_ВсеРегионы";
		"
		|ГДЕ
		|	&ИмяМетаданных.Склад В (&СписокСкладов)";
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "&ИмяМетаданных", ИмяМетаданных);
	Если ИмяМетаданных = "СправочникСклады" Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "Склад В", "Ссылка В");
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СписокСкладов", ПолучитьРазрешенныеСклады(), Истина);
	//ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ЕТС_ВсеРегионы", РольДоступна("ЕТС_ВсеРегионы"), Истина);

КонецПроцедуры

Функция ПолучитьРазрешенныеСклады() Экспорт 

	Запрос = Новый Запрос;
	Если РольДоступна("ЕТС_ВсеРегионы") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Склады.Ссылка КАК Склад
			|ИЗ
			|	Справочник.Склады КАК Склады";
			
	Иначе 			
		Запрос.Текст = 
			"ВЫБРАТЬ Разрешенные
			|	Склады.Ссылка КАК Склад
			|ИЗ
			|	РегистрСведений.ЕТС_ОграничениеНаУровнеЗаписей КАК ЕТС_ОграничениеНаУровнеЗаписей
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
			|		ПО ЕТС_ОграничениеНаУровнеЗаписей.Склад = Склады.БизнесРегион
			|ГДЕ
			|	ЕТС_ОграничениеНаУровнеЗаписей.Пользователь = &Пользователь
			|";
			
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
    СписокСкладов = Новый СписокЗначений;
	СписокСкладов.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Склад"));
	
	Возврат СписокСкладов;
	
КонецФункции // ПолучитьРазрешенныеСклады()

