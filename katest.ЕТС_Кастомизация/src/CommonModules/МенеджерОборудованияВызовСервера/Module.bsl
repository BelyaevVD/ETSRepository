
&ИзменениеИКонтроль("НайтиРабочиеМестаПоИдентификатору")
Функция ЕТС_НайтиРабочиеМестаПоИдентификатору(ИдентификаторКлиента, ИмяКомпьютера, MACАдрес)
	
	Результат = Новый Массив();
	
	Если Не ОбщегоНазначенияБПО.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(MACАдрес) = Тип("Строка") Тогда
		MACАдреса = Новый Массив();
		Если ЗначениеЗаполнено(MACАдрес) Тогда
			MACАдреса.Добавить(MACАдрес);
		КонецЕсли;
	Иначе
		MACАдреса = MACАдрес;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	0 КАК Приоритет,
	|	РабочиеМеста.Ссылка КАК Ссылка,
	|	РабочиеМеста.ИмяКомпьютера КАК ИмяКомпьютера
	|ПОМЕСТИТЬ РабочиеМестаПоПриоритетам
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.Код = &ИдентификаторКлиента
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	РабочиеМестаИдентификаторыКлиентов.Ссылка,
	|	РабочиеМестаИдентификаторыКлиентов.Ссылка.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста.ИдентификаторыКлиентов КАК РабочиеМестаИдентификаторыКлиентов
	|ГДЕ
	|	РабочиеМестаИдентификаторыКлиентов.ИдентификаторКлиента = &ИдентификаторКлиента
	|	И НЕ РабочиеМестаИдентификаторыКлиентов.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	РабочиеМеста.Ссылка,
	|	РабочиеМеста.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.MACАдрес В(&MACАдреса)
	|	И РабочиеМеста.ИмяКомпьютера = &ИмяКомпьютера
	|	И &ИмяКомпьютера <> """"
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	РабочиеМеста.Ссылка,
	|	РабочиеМеста.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.ИмяКомпьютера = &ИмяКомпьютера
	|	И &ИмяКомпьютера <> """"
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|	И РабочиеМеста.MACАдреса.Адрес В(&MACАдреса)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	РабочиеМестаИдентификаторыКлиентов.Ссылка,
	|	РабочиеМестаИдентификаторыКлиентов.Ссылка.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста.ИдентификаторыКлиентов КАК РабочиеМестаИдентификаторыКлиентов
	|ГДЕ
	|	РабочиеМестаИдентификаторыКлиентов.ИдентификаторКлиента = &ИдентификаторКлиента
	|	И РабочиеМестаИдентификаторыКлиентов.Ссылка.MACАдрес В(&MACАдреса)
	|	И НЕ РабочиеМестаИдентификаторыКлиентов.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	РабочиеМеста.Ссылка,
	|	РабочиеМеста.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.ИмяКомпьютера = &ИмяКомпьютера
	|	И &ИмяКомпьютера <> """"
	|	И НЕ РабочиеМеста.ПометкаУдаления
#Вставка
	|	И РабочиеМеста.Наименование ПОДОБНО &ПользовательИБ
#КонецВставки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	РабочиеМеста.Ссылка,
	|	РабочиеМеста.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.MACАдрес В(&MACАдреса)
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	РабочиеМеста.Ссылка,
	|	РабочиеМеста.ИмяКомпьютера
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.MACАдреса.Адрес В(&MACАдреса)
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РабочиеМестаПоПриоритетам.Ссылка КАК Ссылка,
	|	РабочиеМестаПоПриоритетам.ИмяКомпьютера КАК ИмяКомпьютера
	|ИЗ
	|	РабочиеМестаПоПриоритетам КАК РабочиеМестаПоПриоритетам
	|
	|УПОРЯДОЧИТЬ ПО
	|	РабочиеМестаПоПриоритетам.Приоритет");
	
	
	Запрос.УстановитьПараметр("ИдентификаторКлиента", ИдентификаторКлиента);
	Запрос.УстановитьПараметр("ИмяКомпьютера", ИмяКомпьютера);
	Запрос.УстановитьПараметр("MACАдреса", MACАдреса);
#Вставка
	Запрос.УстановитьПараметр("ПользовательИБ", "%" + ПользователиИнформационнойБазы.ТекущийПользователь().Имя + "%");
#КонецВставки
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	РабочееМестоСсылка = Выборка.Ссылка;
	Результат.Добавить(РабочееМестоСсылка);
	
	Если ПустаяСтрока(Выборка.ИмяКомпьютера) И Не ПустаяСтрока(ИмяКомпьютера) Тогда
		РабочееМестоОбъект = РабочееМестоСсылка.ПолучитьОбъект();
		РабочееМестоОбъект.ИмяКомпьютера = ИмяКомпьютера;
		РабочееМестоОбъект.Записать();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
