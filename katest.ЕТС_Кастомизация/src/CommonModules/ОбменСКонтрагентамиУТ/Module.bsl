
&Перед("ЗаполнитьДокументПоступленияУслуг")
Процедура ЕТС_ЗаполнитьДокументПоступленияУслуг(Документ, ДанныеДляЗагрузки, Записывать)

	СтатьяРасхода = Неопределено;
	Подразделение = Неопределено;
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда 
		ДанныеДляЗагрузки.Шапка.Вставить("Дата", ДанныеДляЗагрузки.Шапка.ДатаВходящегоДокумента);

		РеквизитыОП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеДляЗагрузки.Шапка.Контрагент, "ОбособленноеПодразделение, ГоловнойКонтрагент");
		Если РеквизитыОП.ОбособленноеПодразделение Тогда
			Если ЗначениеЗаполнено(РеквизитыОП.ГоловнойКонтрагент) Тогда
				ДанныеДляЗагрузки.Шапка.Вставить("Контрагент", РеквизитыОП.ГоловнойКонтрагент);
				Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОП.ГоловнойКонтрагент,"Партнер");
				ДанныеДляЗагрузки.Шапка.Вставить("Партнер", Партнер);
			КонецЕсли;
		КонецЕсли;

		Если Не ДанныеДляЗагрузки.Шапка.Свойство("Договор") Тогда
			ДанныеДляЗагрузки.Шапка.Вставить("Договор", Неопределено);
		КонецЕсли;

		Если Не ЗначениеЗаполнено(ДанныеДляЗагрузки.Шапка.Договор) Тогда
		
			ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
			ДопПараметры.ВалютаВзаиморасчетов = ДанныеДляЗагрузки.Шапка.Валюта;
			Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ДанныеДляЗагрузки.Шапка, 
														Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика, 
														ДопПараметры);
			Если ЗначениеЗаполнено(Договор) Тогда
            	ДанныеДляЗагрузки.Шапка.Вставить("Договор",Договор);
				РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "Подразделение, ЕТС_СтатьяРасхода");
				ДанныеДляЗагрузки.Шапка.Вставить("Подразделение", РеквизитыДоговора.Подразделение);
				СтатьяРасхода = РеквизитыДоговора.ЕТС_СтатьяРасхода;
				Подразделение = РеквизитыДоговора.Подразделение;
			КонецЕсли;												
																	
		КонецЕсли;
	КонецЕсли;
	Если ДанныеДляЗагрузки.Свойство("Расходы") Тогда 
		Для Каждого стр Из ДанныеДляЗагрузки.Расходы Цикл
			Если стр.Количество = 0 Тогда
				стр.Количество = 1;
				стр.Цена = стр.Сумма;
			КонецЕсли;
			Если стр.Цена = 0 Тогда
				стр.Цена = стр.Сумма;
			КонецЕсли;
			стр.СтатьяРасходов = СтатьяРасхода;
			стр.Подразделение = Подразделение;
			стр.АналитикаРасходов = Подразделение;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры


&ИзменениеИКонтроль("ЗаполнитьДокументПоступленияУслуг")
Процедура ЕТС_ЗаполнитьДокументПоступленияУслуг1(Документ, ДанныеДляЗагрузки, Записывать)

	Текст = "";
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда
		ДанныеЗаполнения = ДанныеДляЗагрузки.Шапка;
	КонецЕсли;

	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ЗначениеЗаполнено(Документ) Тогда // получены изменения по существующему документу
		ДокументОбъект = Документ.ПолучитьОбъект();
		//Попытка заблокировать документ
		Попытка
			ДокументОбъект.Заблокировать();
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить данные документа ""%1"".
			|Возможно, документ редактируется другим пользователем'"),
			Строка(ДокументОбъект.Ссылка));
			ВызватьИсключение ТекстСообщения;
		КонецПопытки;
		//конец попытки заблокировать документ
		Если ДокументОбъект.Проведен Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Текст = НСтр("ru = 'Операция возможна только для непроведенных документов'");
		КонецЕсли;
	Иначе  // создаем новый
		ДокументОбъект = Документы.ПриобретениеУслугПрочихАктивов.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
#Вставка
		Если НачалоДня(ДокументОбъект.Дата) <> НачалоДня(ДанныеЗаполнения.Дата) Тогда 
			ДокументОбъект.Дата = ПолучитьДатуПоследнегоДокумента(ДанныеЗаполнения.Дата);
        КонецЕсли;
#КонецВставки
		ДокументОбъект.Заполнить(ДанныеЗаполнения);
		ДокументОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	ДокументОбъект.ОбменДанными.Загрузка = Истина;

	// попробуем найти заказ поставщику
	Если ДанныеЗаполнения.Свойство("Контрагент") Тогда
		ДокументОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
		РеквизитыИБКонтрагента = Новый Структура;
		Если ДанныеЗаполнения.Свойство("НомерПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымПоставщика) Тогда
			РеквизитыИБКонтрагента.Вставить("НомерПоДаннымПоставщика", ДанныеЗаполнения.НомерПоДаннымПоставщика);
		КонецЕсли;
		Если ДанныеЗаполнения.Свойство("ДатаПоДаннымПоставщика") И ЗначениеЗаполнено(ДанныеЗаполнения.ДатаПоДаннымПоставщика) Тогда
			РеквизитыИБКонтрагента.Вставить("ДатаПоДаннымПоставщика", ДанныеЗаполнения.ДатаПоДаннымПоставщика);
		КонецЕсли;
		РеквизитыИБ = Новый Структура;
		Если ДанныеЗаполнения.Свойство("НомерПоДаннымКлиента") И ЗначениеЗаполнено(ДанныеЗаполнения.НомерПоДаннымКлиента) Тогда
			РеквизитыИБ.Вставить("Номер", ДанныеЗаполнения.НомерПоДаннымКлиента);
		КонецЕсли;
	КонецЕсли;

	// заполнение реквизита ЗакупкаПодДеятельность
	ПараметрыЗаполнения = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыЗаполненияВидаДеятельностиНДС(ДокументОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ДокументОбъект.ЗакупкаПодДеятельность, ПараметрыЗаполнения);

	// вручную переопределим, если требуется
	ПерезаполнениеЗначенийРеквизитовШапки(ДокументОбъект, ДанныеЗаполнения);

	// заполнение соглашения по статистике
	Если Не ЗначениеЗаполнено(ДокументОбъект.Соглашение) Тогда
		ЗаполнитьСоглашениеПоСтатистике(ДокументОбъект);
	КонецЕсли;

	ДокументОбъект.Расходы.Загрузить(ДанныеДляЗагрузки.Расходы);

	Если ДокументОбъект.Расходы.Итог("СуммаНДС") > 0 Тогда
		ДокументОбъект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	Иначе
		ДокументОбъект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
	Конецесли;
	ДокументОбъект.ЦенаВключаетНДС = Ложь;

	Для Каждого Строка Из ДокументОбъект.Расходы Цикл 
		Если ДокументОбъект.ЦенаВключаетНДС И Строка.Сумма <> Строка.СуммаСНДС Тогда
			Строка.Сумма = Строка.СуммаСНДС;
			Если Строка.Количество <> 0 Тогда
				Строка.Цена = Окр(Строка.Сумма/Строка.Количество, 2);
			Иначе
				Строка.Цена = Строка.Сумма;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 

	// сумма документа
	ДокументОбъект.СуммаДокумента = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(ДокументОбъект.Расходы, ДокументОбъект.ЦенаВключаетНДС);

	Попытка
		Если Записывать Тогда
			// запись версии создаваемого документа в подсистеме версионирования
			ДокументОбъект.ОбменДанными.Загрузка = Ложь;
			ВерсионированиеОбъектовСобытия.ЗаписатьВерсиюДокумента(ДокументОбъект, Ложь, РежимЗаписи, РежимПроведенияДокумента.Неоперативный);
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			// запись версии конец

			ДокументОбъект.Записать(РежимЗаписи);
			ДокументОбъект.Разблокировать();
			Документ = ДокументОбъект.Ссылка;

			//Запись в Реестр документов
			ТаблицыДанных = ПроведениеДокументов.ДанныеДокументаДляПроведения(Документ, "РеестрДокументов");
			РегистрыСведений.РеестрДокументов.ЗаписатьДанные(ТаблицыДанных, Документ,  Неопределено, Ложь);
		Иначе
			Документ = ДокументОбъект;
		КонецЕсли;
	Исключение
		ОбщегоНазначения.СообщитьПользователю(Текст);
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заполнение документа на основе ЭД.%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Текст);
		ЗаписьЖурналаРегистрации(Текст, УровеньЖурналаРегистрации.Ошибка, ДокументОбъект.Метаданные(), Документ,
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры


&Перед("ЗаполнитьДокументСчетФактура")
Процедура ЕТС_ЗаполнитьДокументСчетФактура(Документ, ДанныеДляЗагрузки, Записывать)
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда
		Если ДанныеДляЗагрузки.Шапка.ДокументыОснования.Количество()>0 И ТипЗнч(ДанныеДляЗагрузки.Шапка.ДокументыОснования[0]) <> Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			Если ЗначениеЗаполнено(ДанныеДляЗагрузки.Шапка.ДатаВходящегоДокумента) Тогда
				ДанныеДляЗагрузки.Шапка.Вставить("Дата", ПолучитьДатуПоследнегоДокумента(ДанныеДляЗагрузки.Шапка.ДатаВходящегоДокумента));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&Перед("ЗаполнитьДокументПриобретенияТоваровУслуг")
Процедура ЕТС_ЗаполнитьДокументПриобретенияТоваровУслуг(Документ, ДанныеДляЗагрузки, Записывать)
	Если ДанныеДляЗагрузки.Свойство("Шапка") Тогда 
		ДанныеДляЗагрузки.Шапка.Вставить("Дата", ДанныеДляЗагрузки.Шапка.ДатаВходящегоДокумента);
		
		РеквизитыОП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеДляЗагрузки.Шапка.Контрагент, "ОбособленноеПодразделение, ГоловнойКонтрагент");
		Если РеквизитыОП.ОбособленноеПодразделение Тогда
			Если ЗначениеЗаполнено(РеквизитыОП.ГоловнойКонтрагент) Тогда
				ДанныеДляЗагрузки.Шапка.Вставить("Контрагент", РеквизитыОП.ГоловнойКонтрагент);
				Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОП.ГоловнойКонтрагент,"Партнер");
				ДанныеДляЗагрузки.Шапка.Вставить("Партнер", Партнер);
			КонецЕсли;
		КонецЕсли;
		Если Не ДанныеДляЗагрузки.Шапка.Свойство("Договор") Тогда
			ДанныеДляЗагрузки.Шапка.Вставить("Договор", Неопределено);
		КонецЕсли;

		Если Не ЗначениеЗаполнено(ДанныеДляЗагрузки.Шапка.Договор) Тогда
		
			ДопПараметры = ЗакупкиСервер.ДополнительныеПараметрыОтбораДоговоров();
			ДопПараметры.ВалютаВзаиморасчетов = ДанныеДляЗагрузки.Шапка.Валюта;
			Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(ДанныеДляЗагрузки.Шапка, 
														Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика, 
														ДопПараметры);
			Если ЗначениеЗаполнено(Договор) Тогда
            	ДанныеДляЗагрузки.Шапка.Вставить("Договор",Договор);
				РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "Подразделение, ЕТС_СкладПоступления");
				ДанныеДляЗагрузки.Шапка.Вставить("Подразделение", РеквизитыДоговора.Подразделение);
				ДанныеДляЗагрузки.Шапка.Вставить("Склад", РеквизитыДоговора.ЕТС_СкладПоступления);
			КонецЕсли;												
																	
		КонецЕсли;
	КонецЕсли;
	Если ДанныеДляЗагрузки.Свойство("Товары") Тогда 
		Для Каждого стр Из ДанныеДляЗагрузки.Товары Цикл
			
			Если ЗначениеЗаполнено(стр.НомерГТД) Тогда
				НомерГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(стр.НомерГТД,"РегистрационныйНомер");
				Если Не ЗначениеЗаполнено(НомерГТД) Тогда
					стр.НомерГТД = Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьДатуПоследнегоДокумента(Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПеремещениеТоваров.Дата КАК Дата
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
		|ГДЕ
		|	ПеремещениеТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслуг.Дата
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|ГДЕ
		|	ПриобретениеТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриобретениеУслугПрочихАктивов.Дата
		|ИЗ
		|	Документ.ПриобретениеУслугПрочихАктивов КАК ПриобретениеУслугПрочихАктивов
		|ГДЕ
		|	ПриобретениеУслугПрочихАктивов.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроизводствоБезЗаказа.Дата
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
		|ГДЕ
		|	ПроизводствоБезЗаказа.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СборкаТоваров.Дата
		|ИЗ
		|	Документ.СборкаТоваров КАК СборкаТоваров
		|ГДЕ
		|	СборкаТоваров.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Дата
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаможеннаяДекларацияИмпорт.Дата
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
		|ГДЕ
		|	ТаможеннаяДекларацияИмпорт.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Дата
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ГДЕ
		|	ВозвратТоваровПоставщику.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Дата
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ГДЕ
		|	ВозвратТоваровПоставщику.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтКлиента.Дата
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
		|ГДЕ
		|	ВозвратТоваровОтКлиента.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВТ.Дата) КАК Дата
		|ИЗ
		|	ВТ КАК ВТ";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДатаДокумента = Дата;
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Дата) Тогда
		ДатаДокумента = Мин(КонецДня(Дата),Выборка.Дата+1);
	КонецЕсли;
	
	Возврат ДатаДокумента;
	
КонецФункции
